<?xml version="1.0" encoding="utf-8"?>
<s:BorderContainer xmlns:fx="http://ns.adobe.com/mxml/2009"
				   xmlns:s="library://ns.adobe.com/flex/spark"
				   xmlns:mx="library://ns.adobe.com/flex/mx"
				   xmlns:skins="skins.*"
				   minHeight="0"
				   creationComplete="completeHandler(event)">

	<s:layout>
		<s:VerticalLayout horizontalAlign="left"
						  paddingTop="2"
						  paddingBottom="2"
						  paddingLeft="0"
						  paddingRight="0"
						  gap="0"/>
	</s:layout>

	<fx:Script>
		<![CDATA[
			import com.adobe.utils.StringUtil;
			
			import control.URLManager;
			
			import events.SearchEvent;
			
			import model.DataModel;
			import model.ResourceSubscriber;
			
			import modules.exercise.event.ExerciseEvent;
			
			import mx.controls.LinkButton;
			import mx.core.Application;
			import mx.core.FlexGlobals;
			import mx.events.FlexEvent;
			import mx.managers.PopUpManager;
			
			import view.common.ReportInappropriatePopUp;
			
			import vo.ExerciseReportVO;
			import vo.ExerciseScoreVO;
			import vo.ExerciseVO;

			private var _creationComplete:Boolean=false;

			[Bindable]
			private var _exercise:ExerciseVO=null;

			[Bindable]
			private var _tags:Array;

			[Bindable]
			private var _exerciseRating:int=0;

			[Bindable]
			private var _dataAvailable:Boolean=false;

			[Bindable]
			private var _lessAdtInfoStatus:Boolean=false;

			private var reportPopUp:ReportInappropriatePopUp;

			public function completeHandler(event:FlexEvent):void
			{
				_creationComplete=true;
				exerciseData=_exercise;
			}

			public function set exerciseData(exercise:ExerciseVO):void
			{
				_exercise=exercise;
				if (_creationComplete)
				{
					if (_exercise)
					{
					
						_dataAvailable=true;
						updateComponents();
					}
					else
					{
						_dataAvailable=false;
						disableComponents();
					}
				}
			}

			public function get exerciseData():ExerciseVO
			{
				return _exercise;
			}

			private function updateComponents():void
			{
	
				exDescription.text=_exercise.description;
				additionalInfoBox.visible=false;
				additionalInfoBox.includeInLayout=false;
				_lessAdtInfoStatus=false;
				addDescriptors();
				addTagLinkButtons();

				var score:ExerciseScoreVO=new ExerciseScoreVO();
				var report:ExerciseReportVO=new ExerciseReportVO();
				score.exerciseId=_exercise.id;
				report.exerciseId=_exercise.id;

				if (DataModel.getInstance().isLoggedIn)
				{
					//new ExerciseEvent(ExerciseEvent.USER_RATED_EXERCISE, null, null, score).dispatch();
					new ExerciseEvent(ExerciseEvent.USER_REPORTED_EXERCISE, null, report, null).dispatch();
				}
			}

			private function disableComponents():void
			{
				if (DataModel.getInstance().isLoggedIn == false || _dataAvailable == false)
				{
					_exerciseRating=0;
					exDescription.text="";
					additionalInfoBox.visible=false;
					additionalInfoBox.includeInLayout=false;
					_lessAdtInfoStatus=false;
					removeTagLinkButtons();
				}
			}
			
			private function addDescriptors():void{
				removeDescriptors()
				if(_exercise.descriptors && _exercise.descriptors.length){
					var sectTitle:Label=new Label();
					sectTitle.setStyle("fontWeight","bold");
					sectTitle.setStyle("fontSize",11);
					sectTitle.text=resourceManager.getString('myResources','DESCRIPTORS');
					ResourceSubscriber.getInstance().subscribeElement(sectTitle, "text", "myResources", 'DESCRIPTORS');
					
					descriptorContainer.addElement(sectTitle);
					for each(var d:String in _exercise.descriptors){
						var dlabel:IconButton=new IconButton();
						dlabel.id=d;
						dlabel.percentWidth=100;
						dlabel.styleName="commonLinkButton";
						dlabel.setStyle("fontSize", 11);
						dlabel.setStyle("fontWeight", "normal");
						dlabel.label=resourceManager.getString('myResources',d);
						ResourceSubscriber.getInstance().subscribeElement(dlabel, "label", "myResources", d);
						dlabel.addEventListener(MouseEvent.CLICK, descriptorClickHandler);
						descriptorContainer.addElement(dlabel);
					}
				}
			}
			
			private function removeDescriptors():void{
				if(descriptorContainer.numElements){
					ResourceSubscriber.getInstance().unsubscribeContainerElements(descriptorContainer);
					descriptorContainer.removeAllElements();
				}
			}

			private function addTagLinkButtons():void
			{
				removeTagLinkButtons();
				
				if(_exercise.tags){
					_tags = _exercise.tags is Array ? _exercise.tags : (_exercise.tags as String).split(',');

					for each (var tag:String in _tags)
					{
						var linkTag:IconButton=new IconButton();
						linkTag.styleName="commonLinkButton";
						linkTag.setStyle("fontSize", 11);
						linkTag.setStyle("fontWeight", "normal");
						linkTag.label=StringUtil.trim(tag);
						linkTag.addEventListener(MouseEvent.CLICK, tagClickHandler);
						exerciseTagsBox.addElement(linkTag);
					}
				}
			}

			private function removeTagLinkButtons():void
			{
				//tagBoxSeparator.height=0;
				//exerciseTagsBox.removeAllChildren();
				if(exerciseTagsBox.numElements > 0)
					exerciseTagsBox.removeAllElements();
				_tags=null;
			}

			protected function tagClickHandler(event:MouseEvent):void
			{
				DataModel.getInstance().searchField=(event.target as IconButton).label;
				//if (DataModel.getInstance().currentContentViewStackIndex != ViewChangeEvent.VIEWSTACK_SEARCH_MODULE_INDEX)
				//new ViewChangeEvent(ViewChangeEvent.VIEW_SEARCH_MODULE).dispatch();
				URLManager.getInstance().redirect('search');
				new SearchEvent(SearchEvent.LAUNCH_SEARCH).dispatch();
			}
			
			protected function descriptorClickHandler(event:MouseEvent):void{
				DataModel.getInstance().searchField="\""+(event.target as IconButton).label+"\"";
				//new ViewChangeEvent(ViewChangeEvent.VIEW_SEARCH_MODULE).dispatch();
				URLManager.getInstance().redirect('search');
				new SearchEvent(SearchEvent.LAUNCH_SEARCH).dispatch();
			}

			/*
			protected function ratingClickHandler(event:MouseEvent):void
			{
				if (DataModel.getInstance().isLoggedIn && _dataAvailable)
				{
					var score:ExerciseScoreVO=new ExerciseScoreVO();
					score.suggestedScore=rating.rating;
					score.exerciseId=_exercise.id;
					new ExerciseEvent(ExerciseEvent.RATE_EXERCISE, null, null, score).dispatch();
				}
			}*/

			protected function reportVideoClickHandler(event:MouseEvent):void
			{
				if (DataModel.getInstance().isLoggedIn && _dataAvailable)
				{
					reportPopUp=ReportInappropriatePopUp(PopUpManager.createPopUp(FlexGlobals.topLevelApplication.parent, ReportInappropriatePopUp, true));
					PopUpManager.centerPopUp(reportPopUp);
					reportPopUp.exerciseData=_exercise;
				}
			}

			protected function lessAdtInfoClickHandler(event:MouseEvent):void
			{
				additionalInfoBox.visible=!additionalInfoBox.visible;
				additionalInfoBox.includeInLayout=!additionalInfoBox.includeInLayout;
				_lessAdtInfoStatus=!_lessAdtInfoStatus;
			}
		]]>
	</fx:Script>

	<s:HGroup id="ratingAndReportBox"
			  width="100%">
		<s:HGroup paddingTop="4"
				  paddingLeft="0">
			<s:Label id="likescount" text="Likes: 0"/>
			<s:Label id="dislikescount" text="Dislikes: 0"/>
		</s:HGroup>
		<s:HGroup horizontalAlign="right"
				  paddingBottom="0"
				  paddingTop="0"
				  paddingLeft="0"
				  paddingRight="0"
				  width="100%">
			<skins:IconButton id="reportVideo"
							  label="{resourceManager.getString('myResources','REPORT')}"
							  toolTip="{resourceManager.getString('myResources','REPORT_INNAPROPRIATE_VIDEO')}"
							  styleName="reportLinkButton"
							  enabled="{DataModel.getInstance().isLoggedIn &amp;&amp; !DataModel.getInstance().userReportedExercise &amp;&amp; _dataAvailable}"
							  click="reportVideoClickHandler(event)"/>
		</s:HGroup>
	</s:HGroup>
	<s:VGroup id="additionalInfoBox"
			  width="100%"
			  horizontalAlign="left"
			  paddingTop="10"
			  paddingLeft="5"
			  paddingRight="5"
			  paddingBottom="5"
			  visible="false"
			  includeInLayout="false">
		<s:Label id="exDescription"
				 fontSize="11"
				 width="100%"/>
		<s:VGroup id="descriptorContainer" minHeight="0"/>
		<s:Label text="{resourceManager.getString('myResources','TAGS')}"
				 fontWeight="bold"
				 fontSize="11"/>
		<s:TileGroup id="exerciseTagsBox"
					 width="100%"
					 horizontalGap="0"
					 minHeight="0"/>
	</s:VGroup>

	<s:VGroup id="saiBox"
			  width="100%"
			  horizontalAlign="center"
			  gap="0">
		<s:Line width="100%">
			<s:filters>
				<s:DropShadowFilter distance="0"
									angle="0"/>
			</s:filters>
			<s:stroke>
				<s:SolidColorStroke color="#666666"
									weight="1"
									alpha="1"/>
			</s:stroke>
		</s:Line>
		<s:Button id="showLessAdtInfo"
				  fontSize="10"
				  label="{_lessAdtInfoStatus ? resourceManager.getString('myResources','LESS_INFO'): resourceManager.getString('myResources','MORE_INFO') }"
				  cornerRadius="0"
				  skinClass="skins.LessAdtInfoButtonSkin"
				  click="lessAdtInfoClickHandler(event)"/>
	</s:VGroup>

</s:BorderContainer>
