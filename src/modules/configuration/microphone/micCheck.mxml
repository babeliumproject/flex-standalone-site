<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml" width="100%" height="100%" 
	xmlns:ns1="modules.configuration.microphone.*" creationComplete="init()">
	
	<mx:Script>
		<![CDATA[
			
			import modules.configuration.ConfigurationResultEvent;
			import model.DataModel;
			import mx.controls.Alert;		
			
			public var mic:Microphone;
			private var nc:NetConnection;
			public var rec_ns:NetStream;
			public var play_ns:NetStream;
			public var rec:Boolean=false;
			public var timerRec:Timer;
			public var timerPlay:Timer;
			public var onMic:Boolean=false;
            
			private function init():void{								
				nc = new NetConnection();
  				nc.connect("rtmp://" + DataModel.getInstance().server + ":" + DataModel.getInstance().red5Port + "/oflaDemo");
    			nc.addEventListener(NetStatusEvent.NET_STATUS, netStatusHandler);
    			nc.addEventListener(AsyncErrorEvent.ASYNC_ERROR, asyncErrrorHandler); 
    			inicializarTimers(); 	
			}
			
			private function netStatusHandler(e:NetStatusEvent):void{   
				if(e.info.code == "NetConnection.Connect.Success"){
        			rec_ns = new NetStream(nc);
        			play_ns = new NetStream(nc); 
        			rec_ns.addEventListener(AsyncErrorEvent.ASYNC_ERROR,asyncErrrorHandler);
    				play_ns.addEventListener(AsyncErrorEvent.ASYNC_ERROR,asyncErrrorHandler);
				}else   
					//Problemas en la conexion
   					trace(e.info.code); 
			}
			
			private function asyncErrrorHandler(e:AsyncErrorEvent):void{
				//No se usa, pero evita que aparezcan errores con flash player debugg version
			}
			
			private function recStart():void{
				try{
					//Reiniciar conexiones anteriores para la grabacion de varios archivos
					rec_ns.close();
					play_ns.close();
					
					mic=Microphone.getMicrophone();
					
					//Gestion de la deteccion de sonidos
					onMic=false;
					mic.setSilenceLevel(20,1000);
    				mic.addEventListener(ActivityEvent.ACTIVITY, onMicActivity);
    				
					mic.addEventListener(StatusEvent.STATUS,mic_status);
					rec_ns.attachAudio(mic);
					model.DataModel.getInstance().audioRec=true;
					
					/*Se comprueba si se ha grabado un fichero de video en la pestaña web-cam
					o si previamente el usuario ha grabado otro fichero de audio.
					En cualquier caso, el evento StatusEvent.STATUS no se dispara y la grabacion
					de un video no seria posible sin las siguientes lineas de codigo*/
					if (rec || model.DataModel.getInstance().videoRec){
						noSoundText.visible=true;
						DataModel.getInstance().recording=true;
						timerRec.start();
                    	mic.rate = 44;
   						inicializarContador();
   					}					
				}catch(err:Error){
					Alert.show( err.toString() );
				}
			}
			
			private function playStart():void{
				if (rec && onMic){//Rec file exists
					DataModel.getInstance().playing=true;
					inicializarContador();
					statusTextFunc("Playing");
					play_ns.play(DataModel.getInstance().loggedUser.name + "ConfigurationAudio.flv");
					timerPlay.start();
				}else if (rec){//Grabación sin señal
					Alert.show("No se ha detectado señal durante la grabación!");
				}else{//No existe el fichero de grabación
					Alert.show("No has grabado ningun archivo de sonido!");
				}
			}
			
			private function mic_status(e:StatusEvent):void{
            	switch (e.code) {
                    case "Microphone.Muted": //User denied access to micro
                    	var ev:ConfigurationResultEvent = new ConfigurationResultEvent
							(ConfigurationResultEvent.MICROPHONE_RESULT,false);
						dispatchEvent(ev);                   	
                        break;
                    case "Microphone.Unmuted": //User allowed access to micro
                    	noSoundText.visible=true;
                    	DataModel.getInstance().recording=true;
                    	timerRec.start();
                    	mic.rate = 44;
   						inicializarContador();                   
                        break;
                }//switch
            }
			
			private function timerRecFunc(event:TimerEvent):void{				
				var denb:int
				denb = timerRec.currentCount
				if (timerRec.currentCount <= 10){					
					contador.text = denb.toString();
					statusText.text=statusText.text+".";
				}else if(timerRec.currentCount>10){
					DataModel.getInstance().recording=false;
					rec_ns.attachAudio(null);
					esconderInfo();
					rec=true;
					timerRec.reset();//Se prepara para la proxima grabacion
					
					/*Si durante los 10s. de la grabacion, no se ha detectado señal, se lanza
					el evento ConfigurationResultEvent.MICROPHONE_RESULT con valor false para 
					mostrar la X en el panel lateral derecho*/
					if (!onMic){
						var ev:ConfigurationResultEvent = new ConfigurationResultEvent
							(ConfigurationResultEvent.MICROPHONE_RESULT,false);
						dispatchEvent(ev);
					}
				}
			}
			
			private function timerPlayFunc(event:TimerEvent):void{
				var denb:int;
				denb = timerPlay.currentCount;
				if (timerPlay.currentCount <= 10){
					contador.text = denb.toString();
					statusText.text=statusText.text+".";
				}else if (timerPlay.currentCount>10){
					DataModel.getInstance().playing=false;
					var ev:ConfigurationResultEvent = new ConfigurationResultEvent
						(ConfigurationResultEvent.MICROPHONE_RESULT,true);
					dispatchEvent(ev);
					esconderInfo();
					timerPlay.reset();//Se prepara para la proxima reproduccion
				}
			}
			
			private function inicializarTimers():void{
				//Timer utilizado en la grabacion de audio
				timerRec = new Timer(1000,0);
				timerRec.addEventListener(TimerEvent.TIMER, timerRecFunc);
				//Timer utilizado en la reproduccion de audio
				timerPlay = new Timer(1000,0);
				timerPlay.addEventListener(TimerEvent.TIMER, timerPlayFunc);	
			}
			
			private function inicializarContador():void{
				contador.text="0";
				contador.visible=true;
			}
			
			private function esconderInfo():void{
				noSoundText.visible=false;
				statusText.visible=false;
				contador.visible=false;
			}
			
			private function statusTextFunc(s:String):void{
				statusText.text=s;
				statusText.visible=true;
			}
			
			private function onMicActivity(e:ActivityEvent):void{
				/*Solo creamos el fichero flv de sonidos si se lanza el evento: ActivityEvent.ACTIVITY.
				Hay que controlar que solo se crea una vez dicho fichero, y que solo iniciamos una vez
				el texto statusText. Esta funcion se realiza con la variable booleana onMic (Importante
				reiniciar su valor si queremos el correcto funcionamiento de grabaciones posteriores).*/
				if(!onMic){
					rec_ns.publish(DataModel.getInstance().loggedUser.name + "ConfigurationAudio", 'record'); 
					noSoundText.visible=false;
					statusTextFunc("Recording");
					onMic=true;
				}
			}
		]]>
	</mx:Script>
			
					
	<mx:HBox x="10" y="10" width="100%" height="90%" id="allMenu" visible="true">
		
		<mx:VBox id="explicacion" height="90%" width="347">
			<mx:Text/>
			<mx:Text text="COMPROBACION DE MICROFONO" fontSize="17" fontWeight="bold" textDecoration="underline" fontStyle="italic"/>			
			<ns1:explicacionMic height="100%" width="100%" id="texto"/>
		</mx:VBox>
		
		<mx:VBox id="videoPanel" width="400" height="322" horizontalAlign="center" verticalAlign="top">
			<mx:VBox borderStyle="solid" cornerRadius="3" backgroundColor="#e9edee" width="350" height="320"
				horizontalAlign="center" verticalAlign="middle">
				<mx:VBox borderStyle="solid" width="320" height="240" backgroundColor="black">
					<mx:Text id="statusText" text="Recording" color="red" fontSize="20" visible="false"/>
					<mx:Text/>
					<mx:Text/>
					<mx:Text/>
					<mx:Text/>
					<mx:Text id="noSoundText" text="     No hay señal" 
						color="red" fontSize="30" visible="false"/>
					<mx:UIComponent id="videoHolder" width="100%" height="100%"/>
				</mx:VBox>
				<mx:HBox horizontalAlign="center">
					<mx:Button id="recButton" label="REC" click="recStart()" horizontalCenter="true"
						enabled="{!DataModel.getInstance().playing &amp;&amp; 
							!DataModel.getInstance().recording}"/>
					<mx:Button id="playButton" label="PLAY" click="playStart()" horizontalCenter="true"
						enabled="{!DataModel.getInstance().playing &amp;&amp; 
							!DataModel.getInstance().recording}"/>
				</mx:HBox>
				<mx:VBox horizontalAlign="center" fontSize="15">
					<mx:Text id="contador" text="10" visible="false" fontWeight="bold"/>
				</mx:VBox>
			</mx:VBox>			
		</mx:VBox>
		
	</mx:HBox>

	<mx:HBox id="notConect" horizontalAlign="center" verticalAlign="middle" 
		width="100%" height="100%" visible="false">
		<ns1:notMicText/>
	</mx:HBox>
</mx:Canvas>