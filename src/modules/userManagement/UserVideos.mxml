<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009"
		  xmlns:s="library://ns.adobe.com/flex/spark"
		  xmlns:mx="library://ns.adobe.com/flex/mx"
		  width="100%"
		  paddingBottom="16"
		  paddingLeft="16"
		  paddingRight="16"
		  paddingTop="16"
		  creationComplete="creationCompleteHandler(event)"
		  xmlns:userManagement="modules.userManagement.*">
	<fx:Script>
		<![CDATA[
			import events.UserEvent;
			
			import model.DataModel;
			
			import modules.main.VideoPaginator;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.core.FlexGlobals;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			import mx.utils.StringUtil;
			
			import view.common.CustomAlert;
			
			import vo.ExerciseVO;
			import vo.UserVO;

			[Bindable]
			private var dataModel:DataModel=DataModel.getInstance();

			private var currentPageUserVideos:uint=1;
			
			private var _creationComplete:Boolean = false;

			[Bindable]
			private var userVideoList:ArrayCollection;

			private var itemsToDelete:Array=new Array();

			protected function creationCompleteHandler(event:FlexEvent):void
			{
				retrieveVideos();
				_creationComplete = true;
			}

			private function retrieveVideos():void
			{
				currentPageUserVideos=1;
				new UserEvent(UserEvent.RETRIEVE_USER_VIDEOS).dispatch();
			}

			private function set userVideoListRetrieved(value:Boolean):void
			{
				var dataProvider:ArrayCollection=dataModel.userVideoList;
				userVideoList=dataProvider;
				createPagination(userVideoTable, userVideoList, currentPageUserVideos, paginationUserVideos, navigateToUserVideoPage);

			}

			public function createPagination(targetDatagrid:DataGrid, dataProvider:ArrayCollection, currentPageIndicator:uint, paginationContainer:HGroup, pageClickHandler:Function):void
			{
				VideoPaginator.createPaginationMenu(dataProvider.length, dataModel.pageSize, currentPageIndicator, dataModel.numberOfPagesNav, paginationContainer, pageClickHandler);
				refreshDataProvider(targetDatagrid, dataProvider, currentPageIndicator);
			}

			public function refreshDataProvider(targetDatagrid:DataGrid, dataProvider:ArrayCollection, currentPageIndicator:uint):void
			{
				var current:int=currentPageIndicator - 1;
				var pageSize:int=dataModel.pageSize;
				var dataTemp:ArrayCollection=new ArrayCollection(dataProvider.source.slice((current * pageSize), (current * pageSize) + pageSize));
				targetDatagrid.rowCount=dataTemp.length;
				targetDatagrid.dataProvider=dataTemp;
			}

			private function navigateToUserVideoPage(event:MouseEvent):void
			{
				currentPageUserVideos=int((event.target as Button).id);
				createPagination(userVideoTable, userVideoList, currentPageUserVideos, paginationUserVideos, navigateToUserVideoPage);
			}

			protected function deleteSelectedVideos_clickHandler(event:MouseEvent):void
			{
				itemsToDelete=new Array();
				for (var i:int=0; i < userVideoList.length; i++)
				{
					if (userVideoList[i].itemSelected == true)
					{
						itemsToDelete.push(userVideoList[i]);
					}
				}
				if (itemsToDelete.length > 0)
					CustomAlert.confirm(resourceManager.getString('myResources', 'CONFIRM_VIDEO_DELETE'), Alert.YES | Alert.NO, FlexGlobals.topLevelApplication.parent, deleteVideosConfirmation, Alert.NO);
			}

			private function deleteVideosConfirmation(event:CloseEvent):void
			{
				if (event.detail == Alert.YES)
					deleteSelectedVideosConfirmed();
			}

			private function deleteSelectedVideosConfirmed():void
			{
				new UserEvent(UserEvent.DELETE_SELECTED_VIDEOS, itemsToDelete).dispatch();
			}

			private function set selectedVideosDeleted(value:Boolean):void
			{
				if (dataModel.selectedVideosDeleted)
				{
					itemsToDelete=new Array();
					retrieveVideos();
					dataModel.selectedVideosDeleted=false;
				}
			}

			public function resetComponent():void
			{
				currentPageUserVideos=1;
				itemsToDelete=new Array();
				editCancelHandler(null);
				if(_creationComplete){
					retrieveVideos();
				}
			}
			
			public function videoEditRequested(data:ExerciseVO):void{
				videoEditPanel.setInformation(data);
				videoListPanel.includeInLayout = false;
				videoListPanel.visible = false;
				videoEditPanel.includeInLayout = true;
				videoEditPanel.visible = true;
			}
			
			private function editCancelHandler(event:MouseEvent):void{
				videoListPanel.includeInLayout = true;
				videoListPanel.visible = true;
				videoEditPanel.includeInLayout = false;
				videoEditPanel.visible = false;
				videoEditPanel.resetComponent();
			}

			protected function searchButton_clickHandler(event:MouseEvent):void
			{
				// TODO Auto-generated method stub
			}
		]]>
	</fx:Script>

	<fx:Binding source="{dataModel.userVideoListRetrieved}"
				destination="userVideoListRetrieved"/>

	<fx:Binding source="{dataModel.selectedVideosDeleted}"
				destination="selectedVideosDeleted"/>

	<s:VGroup id="videoListPanel"
			  width="100%"
			  height="100%">
		<s:Label id="myVideosTitle"
				 text="{resourceManager.getString('myResources','MY_VIDEOS')}"
				 styleName="sectionTitleLabel"/>
		<s:Line width="{myVideosTitle.width}">
			<s:stroke>
				<mx:SolidColorStroke weight="3"
									 color="{myVideosTitle.getStyle('color')}"/>
			</s:stroke>
		</s:Line>
		<s:Label text="{resourceManager.getString('myResources','MY_VIDEOS_DESCRIPTION')}"
				 paddingTop="10"
				 paddingBottom="10"/>

		<s:BorderContainer id="controlBar"
						   width="100%"
						   minHeight="0"
						   backgroundAlpha="0.5"
						   styleName="roundedBorderGreyBox">

			<s:layout>
				<s:HorizontalLayout verticalAlign="middle"
									paddingBottom="8"
									paddingLeft="8"
									paddingRight="8"
									paddingTop="8"/>
			</s:layout>

			<s:Button id="deleteSelectedVideos"
					  click="deleteSelectedVideos_clickHandler(event)"
					  label="{resourceManager.getString('myResources','DELETE')}"/>
			<mx:VRule height="20"/>
			<s:Label id="videoCount"
					 paddingLeft="16"
					 paddingRight="16"
					 text="{userVideoList.length > 1 ? StringUtil.substitute(resourceManager.getString('myResources','VIDEO_COUNT_MANY'),userVideoList.length) : StringUtil.substitute(resourceManager.getString('myResources','VIDEO_COUNT_SINGLE'),userVideoList.length)}"/>
			<!--
			<mx:VRule height="20"/>
			<s:Label text="{resourceManager.getString('myResources','SORT_BY')+':'}"/>
			<s:DropDownList id="sortOptions"/>
			<s:HGroup width="100%"
					  horizontalAlign="right">
				<s:TextInput id="searchMyVideos"/>
				<s:Button id="searchButton"
						  click="searchButton_clickHandler(event)"
						  label="{resourceManager.getString('myResources','SEARCH')}"/>
			</s:HGroup>
			-->
		</s:BorderContainer>

		<mx:DataGrid id="userVideoTable"
					 dataProvider="{userVideoList}"
					 width="100%"
					 horizontalScrollPolicy="off"
					 verticalScrollPolicy="off"
					 headerHeight="0"
					 horizontalGridLines="true"
					 horizontalGridLineColor="0xaaaaaa"
					 contentBackgroundAlpha="0.0"
					 alternatingItemColors="{[]}"
					 selectable="false"
					 variableRowHeight="true">
			<mx:columns>

				<mx:DataGridColumn itemRenderer="modules.userManagement.UserVideoItemRenderer"/>
			</mx:columns>
		</mx:DataGrid>
		<s:HGroup id="paginationUserVideos"
				  width="100%"
				  horizontalAlign="center"
				  gap="2"/>

	</s:VGroup>

	<userManagement:UserVideoInfoEdit id="videoEditPanel"
									  includeInLayout="false"
									  visible="false"
									  cancelClickHandler="{editCancelHandler}"/>

</s:VGroup>
