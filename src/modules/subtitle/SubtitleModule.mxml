<?xml version="1.0" encoding="utf-8"?>
<s:Module xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  xmlns:mx="library://ns.adobe.com/flex/mx"
		  xmlns:view="modules.subtitle.view.*"
		  width="100%"
		  implements="modules.IModuleInterface" 
		  preinitialize="preinitializeHandler(event)"
		  creationComplete="creationCompleteHandler(event)">
	
	<mx:ViewStack id="moduleViewstack"
				  selectedIndex="{ _viewstackIndex }"
				  resizeToContent="true"
				  width="100%"
				  height="100%">
		<s:NavigatorContent id="manage">
			<view:SubtitleManageView id="subtitleManage" width="100%"/>
		</s:NavigatorContent>
		<s:NavigatorContent id="edit">
			<view:SubtitleEditView id="subtitleEdit" width="100%"/>
		</s:NavigatorContent>
	</mx:ViewStack>
	
	
	
	<fx:Script>
		<![CDATA[
			import control.URLChangeEvent;
			
			import modules.IGroupInterface;
			
			import control.URLManager;
			
			import model.DataModel;
			
			import modules.subtitle.command.SubtitleController;
			import modules.subtitle.event.SubtitleEvent;
			
			import mx.events.FlexEvent;
			
			import spark.components.NavigatorContent;
			
			public var modulename:String = 'subtitle';
			
			[Bindable]
			private var _urlmanager:URLManager=URLManager.getInstance();
			
			[Bindable]
			private var _model:DataModel=DataModel.getInstance();
			
			[Bindable]
			private var _viewstackIndex:uint;
			
			private var _moduleController:SubtitleController;
			private var _creationComplete:Boolean;
			private var _initialUrl:Event;
			
			public static const MANAGE:uint=0;
			public static const EDIT:uint=1;
			
			public function getModuleName():String{
				return modulename;
			}
			
			public function setDataBindings(data:* = null):void{	
				//_moduleController = new SubtitleController();	
				if(data){
					if(data['initialurl']){
						_initialUrl=data['initialurl'];
					}
				}
			}
			
			public function resetModule():void{
				if(_urlmanager) _urlmanager.removeEventListener(URLChangeEvent.CHANGE, onURLChange);
				_urlmanager=null;
				
				this.removeEventListener(FlexEvent.PREINITIALIZE, preinitializeHandler);
				this.removeEventListener(FlexEvent.CREATION_COMPLETE, creationCompleteHandler);
				this.removeEventListener(FlexEvent.ADD, componentAddedHandler);
				this.removeEventListener(FlexEvent.UPDATE_COMPLETE, componentUpdateCompleteHandler);
				
				_moduleController = null;
				_model = null;
				resetViews();
			}
			
			public function resetViews():void{
				if (moduleViewstack)
				{
					var views:Array=moduleViewstack.getChildren();
					for each (var nc:NavigatorContent in views)
					{
						if (nc.deferredContentCreated)
						{
							var v:*=nc.getElementAt(0) as IGroupInterface;
							if (v)
								v.resetGroup();
						}
					}
				}
			}
			
			public function onURLChange(event:Event):void{
				if(!event) return;
				
				var evt:Object=Object(event);
				
				if(!evt.module || evt.module != modulename) return;
				
				//Reset the components before changing the foreground
				resetViews();
				
				switch (evt.action)
				{
					case 'add':
					case 'edit':
						actionEdit(evt.parameters);
						break;
					case 'manage':
						actionManage(evt.parameters);
					default:
						_urlmanager.redirect('/error');
						break;
				}
			}
			
			public function actionAdd(parameters:Object = null):void{
				
			}
			
			public function actionEdit(parameters:Object = null):void{
				if(!parameters){
					_urlmanager.redirect('/error');
				} else {
					var id:String = parameters['id'];
					var subtitles:Object = new Object();
					subtitles.id = id;
					_viewstackIndex=EDIT;
					new SubtitleEvent(SubtitleEvent.GET_MEDIA_SUBTITLES, subtitles).dispatch();
				}
			}
			
			public function actionManage(parameters:Object = null):void{
				if(!parameters){
					_urlmanager.redirect('/error');
				} else {
					var id:String = parameters['id'];
					var p:Object = new Object();
					p.id = id;
					_viewstackIndex=MANAGE;
					new SubtitleEvent(SubtitleEvent.GET_MEDIA_SUBTITLES, p).dispatch();	
				}
			}
			
			protected function preinitializeHandler(event:FlexEvent):void
			{
				this.addEventListener(FlexEvent.ADD, componentAddedHandler, false, 0, true);
				this.addEventListener(FlexEvent.UPDATE_COMPLETE, componentUpdateCompleteHandler, false, 0, true);
			}
			
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				_creationComplete=true;
				if(_urlmanager) _urlmanager.addEventListener(URLChangeEvent.CHANGE, onURLChange, false, 0, true);
				onURLChange(_initialUrl);
			}
			
			protected function componentAddedHandler(event:FlexEvent):void{
				
				this.visible=false;
			}
			
			protected function componentUpdateCompleteHandler(event:FlexEvent):void{
				checkPermissions();
			}
			
			protected function checkPermissions():void{
				if (!DataModel.getInstance().isLoggedIn)
				{
					URLManager.getInstance().redirect('/login');
					return;
				}
				this.visible=true;
			}	
		]]>
	</fx:Script>
	
</s:Module>
