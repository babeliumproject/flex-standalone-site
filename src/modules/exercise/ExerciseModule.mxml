<?xml version="1.0" encoding="utf-8"?>
<s:Module xmlns:fx="http://ns.adobe.com/mxml/2009"
		  xmlns:s="library://ns.adobe.com/flex/spark"
		  xmlns:mx="library://ns.adobe.com/flex/mx"
		  xmlns:view="modules.exercise.view.*"
		  implements="modules.IModuleInterface"
		  width="100%">
	<s:layout>
		<s:VerticalLayout/>
	</s:layout>

	<fx:Script>
		<![CDATA[
			import control.URLManager;
			
			import flash.utils.getDefinitionByName;
			import flash.utils.getQualifiedClassName;
			
			import model.DataModel;
			
			import modules.IGroupInterface;
			import modules.exercise.event.ExerciseEvent;
			import modules.exercise.model.ExerciseModel;
			import modules.exercise.view.ExerciseList;
			import modules.exercise.view.Exercises;
			
			import mx.binding.utils.BindingUtils;
			import mx.binding.utils.ChangeWatcher;
			import mx.core.IVisualElement;
			import mx.core.IVisualElementContainer;
			import mx.utils.ObjectUtil;
			
			import spark.components.Group;
			
			import vo.ExerciseVO;

			public var modulename:String='exercises';

			[Bindable]
			public var urlmanager:URLManager=URLManager.getInstance();

			[Bindable]
			private var viewstackIndex:uint;

			public static const INDEX:uint=0;
			public static const VIEW:uint=1;

			private var changewatcher:ChangeWatcher;

			public function getModuleName():String
			{
				return modulename;
			}

			public function setDataBindings(data:*=null):void
			{
				changewatcher=BindingUtils.bindSetter(onURLChange, urlmanager, "action");
			}

			public function resetModule():void
			{
				changewatcher.unwatch();
				resetViews();
			}

			public function resetViews():void
			{
				if (exerciseViewstack)
				{
					var views:Array=exerciseViewstack.getChildren();
					for each (var nc:NavigatorContent in views)
					{
						if (nc.deferredContentCreated)
						{
							var v:*=nc.getElementAt(0) as IGroupInterface;
							if (v)
								v.resetGroup();
						}
					}
				}
			}

			private function actionView(fragment:String):void
			{
				var ex:ExerciseVO=new ExerciseVO();
				ex.exercisecode=fragment;
				viewstackIndex=VIEW;
				new ExerciseEvent(ExerciseEvent.EXERCISE_SELECTED, ex).dispatch();
			}

			private function actionIndex(parameters:Object=null):void
			{
				viewstackIndex=INDEX;
				
				if(parameters){
					exerciseIndex.sortFilter=parameters.hasOwnProperty('sort') ? parameters.sort : null;
					exerciseIndex.langFilter=parameters.hasOwnProperty('lang') ? parameters.lang : null;
					exerciseIndex.diffFilter=parameters.hasOwnProperty('difficulty') ? parameters.difficulty : 0;
					exerciseIndex.typeFilter=parameters.hasOwnProperty('type') ? parameters.type : 0;
					exerciseIndex.sitFilter=parameters.hasOwnProperty('situation') ? parameters.situation : 0;
					exerciseIndex.searchTerm=parameters.hasOwnProperty('q') ? parameters.q : null;
				}
				
				new ExerciseEvent(ExerciseEvent.GET_RECORDABLE_EXERCISES, parameters).dispatch();
			}

			public function onURLChange(event:Event):void{
				if(!event) return;
				
				var evt:Object=Object(event);
				
				if(!evt.module || evt.module != modulename) return;
				
				//Reset the components before changing the foreground
				resetViews();
				
				switch (evt.action)
				{
					case 'view':
						if (evt.parameters)
						{
							actionView(evt.parameters);
						}
						else
						{
							urlmanager.redirect('home');
							return;
						}
						break;
					case 'filter':
					case 'search':
					default:
						actionIndex(evt.parameters);
						break;
				}
			}
		]]>
	</fx:Script>

	<mx:ViewStack id="exerciseViewstack"
				  selectedIndex="{ viewstackIndex }"
				  resizeToContent="true"
				  width="100%"
				  height="100%">
		<s:NavigatorContent id="index">
			<view:ExerciseList id="exerciseIndex"/>
		</s:NavigatorContent>
		<s:NavigatorContent id="view">
			<view:Exercises id="exerciseView"/>
		</s:NavigatorContent>
	</mx:ViewStack>

</s:Module>
