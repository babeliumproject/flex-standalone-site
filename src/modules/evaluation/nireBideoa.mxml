<?xml version="1.0" encoding="utf-8"?>
<mx:Canvas xmlns:mx="http://www.adobe.com/2006/mxml"
		   xmlns:ns1="modules.evaluation.*"
		   height="100%">

	<mx:Metadata>
	//bideoaren egoera aldaketa bat suertatzen denean altxatzen da
   [ Event( name="myCustomEvent", type="flash.events.Event") ]
   //bideoa bukatzean altxatzen da
   [ Event( name="bukatuDa", type="flash.events.Event") ]
   //play stop edo pause sakatzean altxatzen da
   [ Event( name="botoiaSakatuta", type="flash.events.Event") ]
   //cuepoint bat dagoenean altxatzen da
   [ Event( name="cuePoint", type="events.AddCuepointEvent") ]
</mx:Metadata>


	<mx:Script>
		<![CDATA[
			import events.AddCuepointEvent;
			
			import flash.utils.*;
			
			import model.DataModel;
			
			import mx.collections.*;
			import mx.controls.Alert;
			import mx.controls.Image;
			import mx.controls.Label;
			import mx.events.SliderEvent;
			import mx.skins.halo.ButtonSkin;
			
			import org.gif.player.GIFPlayer;
			
			import view.common.CustomAlert;
			import view.common.TimeFormatter;

			public var vid:Video;
			private var nc:NetConnection;
			private var ns:NetStream;
			private var nsClient:Object={};
			private var iraupena:Number=0;
			private var volumeTransform:SoundTransform;
			private var timer:Timer=null;

			public var bideoIzena:String=new String;
			public var bideoId:int=new int;

			[Bindable]
			public var myGIFPlayer:GIFPlayer;


			private var hasera_da:Boolean=new Boolean;
			private var rol_aukeratua:String=new String;
			public var childTaula:ArrayCollection=new ArrayCollection();

			private var isStarted:Boolean=false;

			private var playbackFinished:Boolean=false;
			private var playbackStarted:Boolean=false;

			[Bindable]
			public var rolTaula:ArrayCollection;

			[Bindable]
			public var cuetaula:ArrayCollection;
			public var cuetimetaula:ArrayCollection;

			public var spr:HBox;
			
			private var timeFormatter:TimeFormatter = new TimeFormatter();

			public function setup():void
			{
				timeFormatter.outputMilliseconds = false;
				myGIFPlayer=new GIFPlayer();
				bufferingImage.source=myGIFPlayer;
				myGIFPlayer.load(new URLRequest(DataModel.getInstance().uploadDomain + "resources/images/buffering.gif"));

				nsClient.onMetaData=this.onMetaData;
				nsClient.onCuePoint=this.onCuePoint;

				nc=new NetConnection();
				nc.connect("rtmp://" + DataModel.getInstance().server + ":" + DataModel.getInstance().red5Port + "/oflaDemo");
				nc.addEventListener(NetStatusEvent.NET_STATUS, netStatusHandler);
				nc.addEventListener(AsyncErrorEvent.ASYNC_ERROR, asyncErrorHandler);

				hasera_da=true;
			}

			private function botoiaSakatu():void
			{
				var botoiaSakatuObj:Event=new Event("botoiaSakatuta");
				dispatchEvent(botoiaSakatuObj);
			}

			public function playVid():void
			{
				ns.resume();
				isStarted=true;
			}

			public function pauseVid():void
			{
				ns.pause();
			}

			public function stopVid():void
			{
				ns.pause();
				ns.seek(0);
				isStarted=false;
			}

			private function set stopVideo(flag:Boolean):void
			{
				if (isStarted)
				{
					stopVid();
				}
			}

			public function setRolAukeratua(izena:String):void
			{
				this.rol_aukeratua=izena;
			}


			public function setBideoIzena(izena:String, bideo_id:int):void
			{
				bideoIzena=izena;
				bideoId=bideo_id;

				setup();
			}

			public function asyncErrorHandler(event:AsyncErrorEvent):void
			{
			}

			private function onMetaData(obj:Object):void
			{
				iraupena=obj.duration;

				elementuakHasieratu();

				cuetaula=new ArrayCollection();
				cuetimetaula=new ArrayCollection();

				for each (var o:Object in obj.cuePoints)
				{
					cuetaula.addItem(o);
					cuetimetaula.addItem(o.time);
				}

				rolTaula=new ArrayCollection();

				for (var i:int=0; i < cuetaula.length; i++)
				{

					if (!rolTaula.contains(cuetaula[i].parameters.izena))
					{
						rolTaula.addItem(cuetaula[i].parameters.izena);
					}
				}


				stopVid();

				//Bolumena piztu haseran, seek egiterakoan ez hotsik entzuteko
				volumeTransform.volume=1
				ns.soundTransform=volumeTransform;

				this.rolAldaketa();
			}

			private function netStatusHandler(event:NetStatusEvent):void
			{
				var info:Object = event.info;
				
				switch(info.code){
					case "NetConnection.Connect.Closed":
						trace("NetConnection Status: The connection was closed successfully.");
						break;
					
					case "NetConnection.Connect.Success":
						trace ("NetConnection Status: The connection attempt succeeded.");
						
						ns=new NetStream(nc);
						ns.play(bideoIzena);
						ns.client=nsClient;
						ns.addEventListener(NetStatusEvent.NET_STATUS, bideoEgoeraAztertu);
						
						vid=new Video();
						vid.attachNetStream(ns);
						uic.addChild(vid);
						
						//Bolumena kontrolatzeko objetuen balioak haseratu
						volumeTransform=new SoundTransform();
						volumeSlider.value=volumeTransform.volume;
						volumeSlider.minimum=0;
						volumeSlider.maximum=1;
						volumeSlider.snapInterval=0.1;
						volumeSlider.tickInterval=volumeSlider.snapInterval;
						volumeSlider.liveDragging=true;
						volumeSlider.addEventListener(SliderEvent.CHANGE, volumeChangeHandler);
						
						//Bolumena ixildu haseran, seek egiterakoan ez hotsik entzuteko
						volumeTransform.volume=0
						ns.soundTransform=volumeTransform;
						
						//segundu laurden bateko maiztasuna erlojua sortu eta hasieratu, segunduro progressbarrak eguneratzeko
						
						timer=new Timer(10, 0);
						timer.addEventListener(TimerEvent.TIMER, onTimer);
						timer.start();
						
						break;
					default:
						trace("NetConnection Error: "+ info.code);
						CustomAlert.error("Error while connecting to the streaming server. Please try again later.");
						break;
				}
			}

			private function bideoEgoeraAztertu(event:NetStatusEvent):void
			{

				//trace("status:" + event.info.code);

				//Hemen egoera ezberdinak dokumentatuta
				//http://livedocs.adobe.com/flash/9.0_es/ActionScriptLangRefV3/flash/events/NetStatusEvent.html
				var myEventObj:Event=new Event("myCustomEvent");
				dispatchEvent(myEventObj);

				switch (event.info.code)
				{

					//Data is not being received quickly enough to fill the buffer.
					case "NetStream.Buffer.Empty":


						if (playbackFinished)
						{
							egoera.text=resourceManager.getString('myResources','MESSAGE_STATUS_FINISHED');
							playbackFinished=false;
							bideoBukaeraTratatu();
						}
						else
						{
							egoera.text=resourceManager.getString('myResources','MESSAGE_STATUS_BUFFERING');
							bufferingScreen.visible=true;
						}
						break;

					//The buffer is full and the stream will begin playing.
					case "NetStream.Buffer.Full":
						bufferingScreen.visible=false;
						egoera.text=resourceManager.getString('myResources','MESSAGE_STATUS_PLAYING');
						break;

					case "NetStream.Play.Stop":
						playbackFinished=true;
						break;

					case "NetStream.Play.Start":
						break;

					case "NetStream.Buffer.Flush":
						break;

					case "NetStream.Pause.Notify":
						break;

					case "NetStream.Unpause.Notify":
						break;
				}


			}


			private function bideoBukaeraTratatu():void
			{
				//bideoa bukatu den Ebentoa altxarazten du
				var bukatuDaObj:Event=new Event("bukatuDa");
				dispatchEvent(bukatuDaObj);

				stopVid();
				volumeTransform.volume=1;
				ns.soundTransform=volumeTransform;

			}

			private function volumeChangeHandler(event:SliderEvent):void
			{
				volumeTransform.volume=event.value;
				ns.soundTransform=volumeTransform;

			}

			private function onTimer(event:TimerEvent):void
			{
				var ehunekoa:int
				//bideoaren progresu barra eguneratu
				bideobarra.setProgress(ns.time, iraupena);
				ehunekoa=(100 * ns.time) / iraupena;
				seek_barra.value=ns.time
				bideobarra.label=resourceManager.getString('myResources', 'CURRENTTIME') + ":   " + timeFormatter.format(ns.time) + "/" + timeFormatter.format(iraupena);


			}


			private function elementuakHasieratu():void
			{

				vid.width= 320 * 0.8;
				vid.height= 240 * 0.8;

				// Resize UIComponent to same size as Video object.
				uic.width=vid.width;
				uic.height=vid.height;

				//seekbarra
				seek_denb.width=bideobarra.width

				seek_barra.minimum=0;
				seek_barra.maximum=iraupena;
				//seek_barra.visible = true

			}

			public function muteatu():void
			{
				volumeTransform.volume=0;
				ns.soundTransform=volumeTransform;
				volumeSlider.enabled=false;
			}

			public function bolumenaAktibatu():void
			{
				volumeTransform.volume=1;
				ns.soundTransform=volumeTransform;
				volumeSlider.enabled=true;
			}



			private function onCuePoint(obj:Object):void
			{


				//cuePoint bat dagoenean altxatuko da
				var myEventObj:AddCuepointEvent=new AddCuepointEvent("cuePoint", obj.parameters.izena);
				dispatchEvent(myEventObj);
			}

			//geziak jartzeko
			public function rolAldaketa():void
			{
				//gorde egingo dugu rola ondoren erabili ahal izateko
				//rol_aukeratua = rolkonboa.selectedItem.toString();	

				trace("R O L   A L D A K E T A");
				for (var i:int=0; i < cuetaula.length; i++)
				{

					var irudia:Image=new Image;

					//trace(rol_aukeratua);
					if (cuetaula[i].parameters.izena == rol_aukeratua)
					{

						irudia.source="resources/images/fletxa_gorri.png";
					}

					irudia.x=panel.x + 10 + ((34 + ((bideobarra.width / iraupena) * (cuetaula[i].time / 1000))) - (17 / 2))
					irudia.y=panel.y + 280 - 6;

					trace("x bideobarra: " + bideobarra.x);
					trace("y bideobarra: " + bideobarra.y);

					childTaula.addItem(this.addChild(irudia));

				}

			}


			public function ezabatuFletxak():void
			{
				for (var i:int=0; i < childTaula.length; i++)
				{

					this.removeChild(childTaula[i]);
				}
				//taulak daukan eduki guztia ezabatu, bestela gero errorea ematen du
				//esistitzen ez diren irudiak ezabatzen hasten baita!

				childTaula=new ArrayCollection();
			}
		]]>
	</mx:Script>

	<mx:Binding source="{DataModel.getInstance().stopVideoFlag}"
				destination="this.stopVideo"/>

	<mx:Panel id="panel"
			  visible="true"
			  horizontalAlign="center"
			  verticalAlign="middle"
			  paddingLeft="0"
			  paddingRight="0"
			  paddingTop="0"
			  paddingBottom="0"
			  headerHeight="0"
			  borderStyle="solid"
			  dropShadowEnabled="true">
		<mx:Label id="egoera"
				  text="{resourceManager.getString('myResources','MESSAGE_STATUS_STOPPED')}"/>
		<mx:HBox x="24"
				 y="541"
				 height="42"
				 verticalAlign="middle"
				 horizontalAlign="center">
			<mx:HBox id="botoiak"
					 dropShadowEnabled="true"
					 borderStyle="solid"
					 cornerRadius="5"
					 x="600"
					 y="265">
				<mx:LinkButton label="Play"
							   click="playVid(); botoiaSakatu();"/>
				<mx:LinkButton label="Pause"
							   click="pauseVid(); botoiaSakatu();"/>
				<mx:LinkButton label="Stop"
							   click="stopVid(); botoiaSakatu();"/>
			</mx:HBox>
			<mx:HBox id="bolumen_kaxa"
					 dropShadowEnabled="true"
					 borderStyle="solid"
					 cornerRadius="5"
					 width="93"
					 height="24"
					 horizontalAlign="center"
					 verticalAlign="middle">
				<mx:Image source="resources/images/bol.png"
						  width="19"
						  height="19"/>
				<mx:HSlider id="volumeSlider"
							width="35"/>
			</mx:HBox>

		</mx:HBox>
		<mx:Canvas horizontalScrollPolicy="off"
				   verticalScrollPolicy="off">
			<mx:UIComponent id="uic"
							width="100%"
							height="100%"/>
			<mx:HBox backgroundColor="#000000"
					 width="100%"
					 height="100%"
					 backgroundAlpha="0.9"
					 id="bufferingScreen"
					 verticalAlign="middle"
					 horizontalAlign="center"
					 visible="false">
				<mx:Image id="bufferingImage"
						  width="31"
						  height="31"/>
				<mx:Text text="{resourceManager.getString('myResources', 'MESSAGE_STATUS_BUFFERING')}"
						 color="#FFFFFF"
						 fontWeight="bold"/>
			</mx:HBox>
		</mx:Canvas>
		<mx:HBox id="fletxak"
				 width="100%"
				 height="35">
		</mx:HBox>

		<!--bideo barraren x eta y balioak aldatu eskero, rolAldaketa funtzioan kontutan hartu behar da -->
		<mx:ProgressBar id="bideobarra"
						x="24"
						y="419"
						mode="manual"/>
		<mx:Label id="pertsonaiaGorriz"
				  text="{resourceManager.getString('myResources', 'YOURCHARACTER')}"
				  textAlign="center"
				  fontWeight="bold"
				  color="#FF0000"
				  height="22"/>

		<mx:HBox id="seek_denb"
				 width="283"
				 height="31"
				 horizontalAlign="center"
				 verticalAlign="middle">
			<mx:HBox width="100%"
					 height="100%"
					 horizontalAlign="center"
					 verticalAlign="middle">
				<mx:Image source="resources/images/ezk.png"/>
				<mx:HSlider minimum="0"
							enabled="true"
							id="seek_barra"
							width="100%"
							height="100%"/>
				<mx:Image source="resources/images/esk.png"/>
			</mx:HBox>
		</mx:HBox>
	</mx:Panel>
</mx:Canvas>
