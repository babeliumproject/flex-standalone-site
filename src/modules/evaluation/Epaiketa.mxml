<?xml version="1.0" encoding="utf-8"?>
<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml"
		 xmlns:ns1="modules.evaluation.*"
		 xmlns:videoPlayer="modules.videoPlayer.*"
		 creationComplete="completeHandler()"
		 xmlns:ns2="modules.autoevaluation.*"
		 paddingBottom="8"
		 fontFamily="Arial"
		 fontSize="12">

	<mx:Script>
		<![CDATA[
			import commands.cuepointManager.RecordingOtherRoleCommand;
			import commands.cuepointManager.ShowHideSubtitleCommand;
			import commands.cuepointManager.StartRecordingSelectedRoleCommand;
			import commands.cuepointManager.StopRecordingSelectedRoleCommand;
			
			import control.BabeliaBrowserManager;
			import control.CuePointManager;
			
			import events.*;
			
			import model.DataModel;
			
			import modules.videoPlayer.events.VideoPlayerEvent;
			import modules.videoPlayer.events.babelia.StreamEvent;
			import modules.videoPlayer.events.babelia.VideoPlayerBabeliaEvent;
			
			import mx.collections.ArrayCollection;
			import mx.events.ListEvent;
			import mx.managers.PopUpManager;
			import mx.resources.ResourceManager;
			
			import view.common.CustomAlert;
			import view.common.TimeFormatter;
			
			import vo.CreditHistoryVO;
			import vo.CueObject;
			import vo.EvaluationVO;

			[Bindable]
			private var dataModel:DataModel=DataModel.getInstance();

			private var _cueManager:CuePointManager=CuePointManager.getInstance();
			private var _cueManagerReady:Boolean=false;

			private var _timeFormatter:TimeFormatter=new TimeFormatter();

			[Bindable]
			private var _streamSource:String=DataModel.getInstance().streamingResourcesPath;
			[Bindable]
			private var _exerciseTitle:String=ResourceManager.getInstance().getString('myResources', 'LABEL_EXERCISE_TITLE');
			[Bindable]
			private var _evaluationSelected:Boolean=false;

			private var _videoPlayerReady:Boolean=false;

			private var _exerciseId:int;
			private var _exerciseName:String;

			private var _responseId:int;
			private var _responseName:String;
			private var _responseCharacterName:String;
			private var _responseSubtitleId:int;

			private var _userId:int;

			private var _videoCommentName:String;
			private var _score:int;
			private var _comment:String;

			[Bindable]
			private var assessmentDetailList:ArrayCollection;
			[Bindable]
			private var waitingForAssessmentList:ArrayCollection;
			[Bindable]
			private var assessedToCurrentUserList:ArrayCollection;
			[Bindable]
			private var assessedByCurrentUserList:ArrayCollection;


			private var biakBatera:Boolean=false;
			private var oraingoTab:String="hasiera";
			private var kont:int=new int;
			private var niriEp:Boolean=false;
			private var nikEp:Boolean=false;

			/**
			 *  Visual component declaration
			 */

			//public var assessmentDetailDataGrid:DataGrid = new DataGrid();
			//public var waitingAssessmentDataGrid:DataGrid = new DataGrid();
			//public var assessedToCurrentUserDataGrid:DataGrid = new DataGrid();
			//public var assessedByCurrentUserDataGrid:DataGrid = new DataGrid();

			/**
			 * Constructor
			 */
			public function completeHandler():void
			{
				_timeFormatter.outputMilliseconds=false;

				refreshEvaluationData();

				setupVideoPlayer();
			}

			private function setupVideoPlayer():void
			{
				VP.addEventListener(VideoPlayerEvent.CONNECTED, onVideoPlayerReady);
				VP.addEventListener(VideoPlayerBabeliaEvent.SECONDSTREAM_FINISHED_PLAYING, onResponseFinished);
			}

			private function onVideoPlayerReady(e:VideoPlayerEvent):void
			{
				_videoPlayerReady=true;
				VP.stopVideo();
			}

			private function set waitingAssessmentRetrievedHandler(value:Boolean):void
			{
				var waDataprovider:ArrayCollection=dataModel.waitingForAssessmentData;
				waitingAssessmentDataGrid.rowCount=waDataprovider.length;
				waitingForAssessmentList=waDataprovider;
				allDataReceived();
			}

			private function set assessedByCurrentUserRetrievedHandler(value:Boolean):void
			{
				var abcuDataprovider:ArrayCollection=dataModel.assessedByCurrentUserData;
				assessedByCurrentUserDataGrid.rowCount=abcuDataprovider.length;
				assessedByCurrentUserList=abcuDataprovider;
				allDataReceived();
			}

			private function set assessedToCurrentUserRetrievedHandler(value:Boolean):void
			{
				var atcuDataprovider:ArrayCollection=dataModel.assessedToCurrentUserData;
				assessedToCurrentUserDataGrid.rowCount=atcuDataprovider.length;
				assessedToCurrentUserList=atcuDataprovider;
				allDataReceived();
			}

			private function set detailsOfAssessmentRetrievedHandler(value:Boolean):void
			{
				var doaDataprovider:ArrayCollection=dataModel.detailsOfAssessedResponseData;
				assessmentDetailDataGrid.rowCount=doaDataprovider.length;
				assessmentDetailList=doaDataprovider;
			}

			private function set assessmentSavedHandler(value:Boolean):void
			{

				ratingBox.setVisible(false);

				var creditHistory:CreditHistoryVO=new CreditHistoryVO();
				creditHistory.videoExerciseId=_exerciseId;
				creditHistory.videoResponseId=int(_responseId);
				DataModel.getInstance().historicData=creditHistory;
				new CreditEvent(CreditEvent.ADD_CREDITS_FOR_EVALUATING, DataModel.getInstance().loggedUser.id).dispatch();

				refreshEvaluationData();
			}

			private function refreshEvaluationData():void
			{
				if (dataModel.isLoggedIn)
				{
					_userId=dataModel.loggedUser.id;
					new EvaluationEvent(EvaluationEvent.GET_RESPONSES_WAITING_ASSESSMENT, null, _userId).dispatch();
					new EvaluationEvent(EvaluationEvent.GET_RESPONSES_ASSESSED_TO_CURRENT_USER, null, _userId).dispatch();
					new EvaluationEvent(EvaluationEvent.GET_RESPONSES_ASSESSED_BY_CURRENT_USER, null, _userId).dispatch();
				}
				else
				{
					_userId=0;
					waitingForAssessmentList=new ArrayCollection();
					assessedToCurrentUserList=new ArrayCollection();
					assessedByCurrentUserList=new ArrayCollection();
					assessmentDetailList=new ArrayCollection();
				}
				assessmentsOfSelectedResponse.visible=false;
				ratingBox.visible=false;
			}

			private function prepareEvaluation():void
			{
				// Prepare new video in VideoPlayer
				VP.stopVideo();
				VP.state=VideoPlayerBabelia.PLAY_BOTH_STATE;
				VP.videoSource=_exerciseName;
				VP.secondSource=_responseName;
				VP.refresh();


				hideArrows();
				resetCueManager();
				prepareCueManager();
			}

			private function resetCueManager():void
			{
				_cueManager.reset();
				VP.removeEventListener(StreamEvent.ENTER_FRAME, _cueManager.monitorCuePoints);
				_cueManager.removeEventListener(CueManagerEvent.SUBTITLES_RETRIEVED, onSubtitlesRetrieved);
			}

			private function prepareCueManager():void
			{

				_cueManager.addEventListener(CueManagerEvent.SUBTITLES_RETRIEVED, onSubtitlesRetrieved);
				_cueManager.setCuesFromSubtitleUsingId(_responseSubtitleId);
				//set cues from subtitle id retrieved from the list

				VP.removeEventListener(StreamEvent.ENTER_FRAME, _cueManager.monitorCuePoints);
				VP.addEventListener(StreamEvent.ENTER_FRAME, _cueManager.monitorCuePoints);
			}

			private function onSubtitlesRetrieved(e:CueManagerEvent):void
			{
				showArrows();
				setupSimultaneousPlaybackCommands();
			}

			private function setupExercisePlaybackCommands():void
			{
				var auxList:ArrayCollection=_cueManager.getCuelist();

				if (auxList.length <= 0)
					return;

				for each (var cueobj:CueObject in auxList)
				{
					cueobj.setStartCommand(new ShowHideSubtitleCommand(cueobj, VP));
					cueobj.setEndCommand(new ShowHideSubtitleCommand(null, VP));
				}

				_cueManagerReady=true;
			}

			private function setupResponsePlaybackCommands():void
			{
				var auxList:ArrayCollection=_cueManager.getCuelist();

				if (auxList.length <= 0)
					return;

				for each (var cueobj:CueObject in auxList)
				{
					cueobj.setStartCommand(new ShowHideSubtitleCommand(cueobj, VP));
					cueobj.setEndCommand(new ShowHideSubtitleCommand(null, VP));
				}

				_cueManagerReady=true;
			}

			private function setupSimultaneousPlaybackCommands():void
			{
				var auxList:ArrayCollection=_cueManager.getCuelist();

				if (auxList.length <= 0)
					return;

				for each (var cueobj:CueObject in auxList)
				{
					if (cueobj.getRole() != _responseCharacterName)
					{
						cueobj.setStartCommand(new RecordingOtherRoleCommand(cueobj.getText(), cueobj.getRole(), cueobj.getEndTime() - cueobj.getStartTime(), VP));

						cueobj.setEndCommand(new ShowHideSubtitleCommand(null, VP));
					}
					else
					{
						cueobj.setStartCommand(new StartRecordingSelectedRoleCommand(cueobj.getText(), _responseCharacterName, cueobj.getEndTime() - cueobj.getStartTime(), VP));

						cueobj.setEndCommand(new StopRecordingSelectedRoleCommand(VP));
					}
				}

				_cueManagerReady=true;
			}

			private function showArrows():void
			{
				VP.arrows=true;
				VP.setArrows(_cueManager.cues2rolearray(), _responseCharacterName);
			}

			private function hideArrows():void
			{
				VP.arrows=false;
				VP.removeArrows();
			}

			private function sendAssessmentClickHandler():void
			{
				if ((ratingRadioGroup.selectedValue) && (commentsTextArea.text != ""))
				{
					_score=ratingRadioGroup.selectedValue as int;
					_comment=commentsTextArea.text;
					var evaluation:EvaluationVO=new EvaluationVO();
					evaluation.score=_score;
					evaluation.comment=_comment;
					evaluation.responseId=_responseId;
					evaluation.userId=_userId;
					evaluation.evaluationVideoFileIdentifier=_videoCommentName;

					//If the user added a video comment
					if (videoCommentCheckBox.selected)
						new EvaluationEvent(EvaluationEvent.ADD_VIDEO_ASSESSMENT, evaluation).dispatch();
					else
						new EvaluationEvent(EvaluationEvent.ADD_ASSESSMENT, evaluation).dispatch();
					reset();
				}
				else
				{
					CustomAlert.info(resourceManager.getString('myResources', 'YOUMUSTSPECIFYASCORE'));
				}
			}

			//Epaiketari dagokion "reset" botoia sakatzean exekutatuko den funtzioa
			private function reset():void
			{
				ratingRadioGroup.selection=null;
				commentsTextArea.text="";
			}

			private function viewGraphicClickHandler():void
			{
				Grafikoak.responseId=_responseId;
				var grafikoa:Grafikoak=Grafikoak(PopUpManager.createPopUp(this, Grafikoak, true));
				PopUpManager.centerPopUp(grafikoa);
			}

			private function addVideoComment():void
			{
				var videoCommentPopUp:BideoGrabaketa=BideoGrabaketa(PopUpManager.createPopUp(this, BideoGrabaketa, true));

				videoCommentPopUp.addEventListener(AddVideoEvent.CHANGE_OPTION, bideoaGehituta);
				PopUpManager.centerPopUp(videoCommentPopUp);

			}

			private function bideoaGehituta(event:AddVideoEvent):void
			{
				addVideoCommentButton.visible=false;
				addVideoCommentLabel.visible=true;
				videoCommentCheckBox.visible=true;
				videoCommentCheckBox.label=event.bideoIzena.toString();
				_videoCommentName=event.bideoIzena.toString();
			}

			private function onResponseFinished(event:Event):void
			{
				if (evaluationOptionsViewStack.selectedChild == waitingAssessmentBox)
					sendAssessmentButton.enabled=true;
			}

			private function allDataReceived():void
			{
				if (waitingForAssessmentList != null && assessedByCurrentUserList != null && assessedByCurrentUserList != null)
				{
					onURLChange="Data";
				}
			}

			private function waitingAssessmentChangeHandler(event:Event):void
			{
				var selectedItem:EvaluationVO=(DataGrid(event.target).selectedItem) as EvaluationVO;
				_exerciseId=selectedItem.exerciseId;
				_exerciseName=selectedItem.exerciseName;
				_responseCharacterName=selectedItem.responseCharacterName;
				_responseId=selectedItem.responseId;
				_responseName=selectedItem.responseFileIdentifier;
				_responseSubtitleId=selectedItem.responseSubtitleId;

				_exerciseTitle=selectedItem.exerciseTitle;

				//Retrieve the associated subtitles and prepare the videoplayer
				prepareEvaluation();
				_evaluationSelected=true;


				ratingBox.visible=true;
				addVideoCommentButton.visible=true;
				addVideoCommentLabel.visible=false;
				videoCommentCheckBox.visible=false;
				sendAssessmentButton.enabled=false;

				new EvaluationEvent(EvaluationEvent.DETAILS_OF_ASSESSED_RESPONSE, null, 0, _responseId).dispatch();

				var urlResponseName:String=_responseName.replace("audio/", "");

				BabeliaBrowserManager.getInstance().updateURL(BabeliaBrowserManager.index2fragment(ViewChangeEvent.VIEWSTACK_EVALUATION_MODULE_INDEX), BabeliaBrowserManager.EVALUATE, urlResponseName);

			}

			private function assessedToCurrentUserChangeHandler(event:Event):void
			{
				var selectedItem:EvaluationVO=(DataGrid(event.target).selectedItem) as EvaluationVO;
				_exerciseId=selectedItem.exerciseId;
				_exerciseName=selectedItem.exerciseName;
				_responseCharacterName=selectedItem.responseCharacterName;
				_responseId=selectedItem.responseId;
				_responseName=selectedItem.responseFileIdentifier;
				_responseSubtitleId=selectedItem.responseSubtitleId;

				_exerciseTitle=selectedItem.exerciseTitle;
				_evaluationSelected=true;

				//Retrieve the associated subtitles and prepare the videoplayer
				prepareEvaluation();


				averageRating.text=resourceManager.getString('myResources', 'AVG') + ": " + selectedItem.evaluationAverage;

				assessmentsOfSelectedResponse.visible=true;

				new EvaluationEvent(EvaluationEvent.DETAILS_OF_ASSESSED_RESPONSE, null, 0, _responseId).dispatch();

				//Get the autoevaluation info if available
				computerEvaluation.setResponseID(_responseId);

				var urlResponseName:String=_responseName.replace("audio/", "");

				BabeliaBrowserManager.getInstance().updateURL(BabeliaBrowserManager.index2fragment(ViewChangeEvent.VIEWSTACK_EVALUATION_MODULE_INDEX), BabeliaBrowserManager.REVISE, urlResponseName);
			}

			private function assessedByCurrentUserChangeHandler(event:Event):void
			{
				var selectedItem:EvaluationVO=(DataGrid(event.target).selectedItem) as EvaluationVO;
				_exerciseId=selectedItem.exerciseId;
				_exerciseName=selectedItem.exerciseName;
				_responseCharacterName=selectedItem.responseCharacterName;
				_responseId=selectedItem.responseId;
				_responseName=selectedItem.responseFileIdentifier;
				_responseSubtitleId=selectedItem.responseSubtitleId;

				_exerciseTitle=selectedItem.exerciseTitle;
				_evaluationSelected=true;

				//Retrieve the associated subtitles and prepare the videoplayer
				prepareEvaluation();

				addVideoCommentButton.visible=true;
				addVideoCommentLabel.visible=false;
				videoCommentCheckBox.visible=false;

				var urlResponseName:String=_responseName.replace("audio/", "");

				BabeliaBrowserManager.getInstance().updateURL(BabeliaBrowserManager.index2fragment(ViewChangeEvent.VIEWSTACK_EVALUATION_MODULE_INDEX), BabeliaBrowserManager.VIEW, urlResponseName);
			}

			private function assessmentDetailChangeHandler(event:Event):void
			{
				var selectedItem:EvaluationVO=(DataGrid(event.target).selectedItem) as EvaluationVO;
				if (selectedItem.evaluationVideoFileIdentifier)
				{
					_videoCommentName=selectedItem.evaluationVideoFileIdentifier;
					var bideoIruzkin:bideoaIkusi=bideoaIkusi(PopUpManager.createPopUp(this, bideoaIkusi, true));
					PopUpManager.centerPopUp(bideoIruzkin);
					assessmentDetailDataGrid.selectedItem=null;
					bideoIruzkin.setBideoIzena(_videoCommentName);
				}
			}

			private function set showAutoevalTab(val:Boolean):void
			{
				if (evaluationTypeNavigator != null)
				{
					//Show the autoevaluation tab if autoevaluation is available, else hide it
					evaluationTypeNavigator.getTabAt(1).visible=DataModel.getInstance().autoevaluationAvailable;
					evaluationTypeNavigator.getTabAt(1).enabled=DataModel.getInstance().autoevaluationAvailable;
					evaluationTypeNavigator.getTabAt(1).includeInLayout=DataModel.getInstance().autoevaluationAvailable;
					evaluationTypeNavigator.selectedIndex=0;
				}
				else
					DataModel.getInstance().autoevaluationAvailable=true;
			}

			private function durationLabelFunction(item:Object, column:DataGridColumn):String
			{
				if (item)
					return _timeFormatter.format(item.exerciseDuration);
				else
					return "";
			}

			private function onEvaluationTabChange(event:MouseEvent):void
			{
				var pressedButton:Object = event.currentTarget;
				switch(pressedButton){
					case waitingAssessmentButton:
						waitingAssessmentButton.setStyle('fillColors',[0xFFFFFF,0x6FA3D3]);
						assessedToCurrentUserButton.setStyle('fillColors',[0xFFFFFF,0xCCCCCC]);
						assessedByCurrentUserButton.setStyle('fillColors',[0xFFFFFF,0xCCCCCC]);
						assessedToCurrentUserDataGrid.selectedItem = null;
						assessedByCurrentUserDataGrid.selectedItem = null;
						assessmentsOfSelectedResponse.visible = false;
						VP.stopVideo();
						VP.videoSource="";
						hideArrows();
						resetCueManager();
						evaluationOptionsViewStack.selectedChild = waitingAssessmentBox;
						break;
					case assessedToCurrentUserButton:
						waitingAssessmentButton.setStyle('fillColors',[0xFFFFFF,0xCCCCCC]);
						assessedToCurrentUserButton.setStyle('fillColors',[0xFFFFFF,0x6FA3D3]);
						assessedByCurrentUserButton.setStyle('fillColors',[0xFFFFFF,0xCCCCCC]);
						waitingAssessmentDataGrid.selectedItem = null;
						assessedByCurrentUserDataGrid.selectedItem = null;
						ratingBox.visible = false;
						VP.stopVideo();
						VP.videoSource="";
						hideArrows();
						resetCueManager();
						evaluationOptionsViewStack.selectedChild = assessedToCurrentUserBox;
						break;
					case assessedByCurrentUserButton:
						waitingAssessmentButton.setStyle('fillColors',[0xFFFFFF,0xCCCCCC]);
						assessedToCurrentUserButton.setStyle('fillColors',[0xFFFFFF,0xCCCCCC]);
						assessedByCurrentUserButton.setStyle('fillColors',[0xFFFFFF,0x6FA3D3]);
						waitingAssessmentDataGrid.selectedItem = null;
						assessedToCurrentUserDataGrid.selectedItem = null;
						assessmentsOfSelectedResponse.visible = false;
						ratingBox.visible = false;
						VP.stopVideo();
						VP.videoSource="";
						hideArrows();
						resetCueManager();
						evaluationOptionsViewStack.selectedChild = assessedByCurrentUserBox;
						break;
					default:
						break;
				}
			}

			private function set userAuthenticationHandler(value:Boolean):void
			{
				refreshEvaluationData();
				VP.stopVideo();
				VP.videoSource="";
				hideArrows();
				resetCueManager();
				waitingAssessmentButton.dispatchEvent(new MouseEvent(MouseEvent.CLICK));
			}

			private function set onTabChange(value:Boolean):void
			{
				ratingBox.visible=false;
				assessmentsOfSelectedResponse.visible=false;

				VP.stopVideo();
				VP.videoSource="";
				hideArrows();
				resetCueManager();
				waitingAssessmentButton.dispatchEvent(new MouseEvent(MouseEvent.CLICK));
			}

			private function set onURLChange(value:Object):void
			{
				var browser:BabeliaBrowserManager=BabeliaBrowserManager.getInstance();

				if (browser.moduleIndex != ViewChangeEvent.VIEWSTACK_EVALUATION_MODULE_INDEX)
					return;

				if (value == null)
					return;

				if (!dataModel.isLoggedIn)
				{
					//new ViewChangeEvent(ViewChangeEvent.VIEW_EVALUATION_MODULE).dispatch();
					return;
				}

				
				if (waitingForAssessmentList == null || assessedByCurrentUserList == null || assessedToCurrentUserList == null)
				{
					//new ViewChangeEvent(ViewChangeEvent.VIEW_EVALUATION_MODULE).dispatch();
					return;
				}



				switch (browser.actionFragment)
				{
					case BabeliaBrowserManager.EVALUATE:
						if (browser.targetFragment != '')
						{
							var tmpEvaluate:EvaluationVO;
							for each (var evEvaluate:EvaluationVO in waitingForAssessmentList)
							{
								var evaluateStripped:String=evEvaluate.responseFileIdentifier.replace("audio/", "");

								if (evaluateStripped == browser.targetFragment)
								{
									tmpEvaluate=evEvaluate;
									break;
								}
							}
							if (tmpEvaluate)
							{
								//Go to the speficied tab and dispatch a change event
								waitingAssessmentButton.dispatchEvent(new MouseEvent(MouseEvent.CLICK));
								waitingAssessmentDataGrid.selectedItem=tmpEvaluate;
								waitingAssessmentDataGrid.dispatchEvent(new ListEvent(ListEvent.CHANGE));
							}
							else
							{
								new ViewChangeEvent(ViewChangeEvent.VIEW_EVALUATION_MODULE).dispatch();
							}
						}
						break;

					case BabeliaBrowserManager.REVISE:
						if (browser.targetFragment != '')
						{
							evaluationOptionsViewStack.selectedChild=assessedToCurrentUserBox;
							var tmpRevise:EvaluationVO;
							for each (var evRevise:EvaluationVO in assessedToCurrentUserList)
							{
								var reviseStripped:String=evRevise.responseFileIdentifier.replace("audio/", "");

								if (reviseStripped == browser.targetFragment)
								{
									tmpRevise=evRevise;
									break;
								}
							}
							if (tmpRevise)
							{
								//Go to the speficied tab and dispatch a change event
								assessedToCurrentUserButton.dispatchEvent(new MouseEvent(MouseEvent.CLICK));
								assessedToCurrentUserDataGrid.selectedItem=tmpRevise;
								assessedToCurrentUserDataGrid.dispatchEvent(new ListEvent(ListEvent.CHANGE));
							}
							else
							{
								new ViewChangeEvent(ViewChangeEvent.VIEW_HOME_MODULE).dispatch();
							}
						}
						break;

					case BabeliaBrowserManager.VIEW:

						if (browser.targetFragment != '')
						{
							var tmpView:EvaluationVO;
							for each (var evView:EvaluationVO in assessedByCurrentUserList)
							{
								var viewStripped:String=evView.responseFileIdentifier.replace("audio/", "");
								if (viewStripped == browser.targetFragment)
								{
									tmpView=evView;
									break;
								}
							}
							if (tmpView != null)
							{
								//Go to the speficied tab and dispatch a change event
								assessedByCurrentUserButton.dispatchEvent(new MouseEvent(MouseEvent.CLICK));
								assessedByCurrentUserDataGrid.selectedItem=tmpView;
								assessedByCurrentUserDataGrid.dispatchEvent(new ListEvent(ListEvent.CHANGE));
							}
							else
							{
								new ViewChangeEvent(ViewChangeEvent.VIEW_HOME_MODULE).dispatch();
							}
						}
						break;
					default:
						new ViewChangeEvent(ViewChangeEvent.VIEW_EVALUATION_MODULE).dispatch();
						break;
				}
			}
		]]>
	</mx:Script>

	<mx:Binding source="{DataModel.getInstance().autoevaluationAvailable}"
				destination="showAutoevalTab"/>
	<mx:Binding source="{DataModel.getInstance().isLoggedIn}"
				destination="userAuthenticationHandler"/>
	<mx:Binding source="{DataModel.getInstance().stopVideoFlag}"
				destination="onTabChange"/>

	<mx:Binding source="{dataModel.waitingForAssessmentDataRetrieved}"
				destination="waitingAssessmentRetrievedHandler"/>
	<mx:Binding source="{dataModel.assessedToCurrentUserDataRetrieved}"
				destination="assessedToCurrentUserRetrievedHandler"/>
	<mx:Binding source="{dataModel.assessedByCurrentUserDataRetrieved}"
				destination="assessedByCurrentUserRetrievedHandler"/>
	<mx:Binding source="{dataModel.detailsOfAssessedResponseDataRetrieved}"
				destination="detailsOfAssessmentRetrievedHandler"/>

	<mx:Binding source="{dataModel.addAssessmentRetrieved}"
				destination="assessmentSavedHandler"/>


	<mx:Binding source="{BabeliaBrowserManager.getInstance().targetFragment}"
				destination="onURLChange"/>

	<mx:VBox id="videoPlayerBox">
		<mx:Label id="exerciseTitle"
				  text="{_exerciseTitle}"
				  width="100%"
				  fontSize="16"
				  fontWeight="bold"/>
		<!-- w/h = 16:10 -->
		<videoPlayer:VideoPlayerBabelia id="VP"
										autoPlay="true"
										skin="white"
										seek="true"
										subtitles="true"
										autoScale="false"
										arrows="false"
										subtitlingControls="false"
										width="500"
										height="{Math.floor(VP.width * 10 / 16)}"
										streamSource="{_streamSource}"
										controlsEnabled="{_evaluationSelected}"/>
	</mx:VBox>
	<mx:Spacer width="20"/>

	<mx:VBox width="100%"
			 height="100%"
			 paddingLeft="4"
			 paddingRight="4"
			 paddingBottom="4">
		<mx:HBox width="100%"
				 height="35"
				 horizontalAlign="center"
				 verticalAlign="middle"
				 cornerRadius="8"
				 backgroundColor="#DDDDDD"
				 borderStyle="solid"
				 horizontalGap="16">
			<mx:Button id="waitingAssessmentButton"
					   label="{resourceManager.getString('myResources','EPAITUGABEDAUDENAK')}"
					   cornerRadius="8"
					   click="onEvaluationTabChange(event)"/>
			<mx:Button id="assessedToCurrentUserButton"
					   label="{resourceManager.getString('myResources','NIRIPAITUTAKOAK')}"
					   cornerRadius="8"
					   click="onEvaluationTabChange(event)"/>
			<mx:Button id="assessedByCurrentUserButton"
					   label="{resourceManager.getString('myResources','NIKEPAITUTAKOAK')}"
					   cornerRadius="8"
					   click="onEvaluationTabChange(event)"/>
		</mx:HBox>

		<mx:ViewStack id="evaluationOptionsViewStack"
					  width="100%"
					  height="100%"
					  creationPolicy="all">

			<!-- Epaitu gabe dauden bideoak aukeratzeko zerrenda erakutsiko du -->
			<mx:VBox id="waitingAssessmentBox"
					 horizontalAlign="center">
				<mx:DataGrid id="waitingAssessmentDataGrid"
							 dataProvider="{waitingForAssessmentList}"
							 width="100%"
							 horizontalScrollPolicy="off"
							 verticalScrollPolicy="off"
							 verticalAlign="middle"
							 change="waitingAssessmentChangeHandler(event)">
					<mx:columns>
						<mx:DataGridColumn headerText="{resourceManager.getString('myResources', 'JATORRIZKOA')}"
										   dataField="exerciseThumbnailUri"
										   width="{120*0.8}">
							<mx:itemRenderer>
								<mx:Component>
									<mx:VBox>
										<mx:Image id="albumImage"
												  height="{90*0.8}"
												  width="{120*0.8}"
												  source="resources/images/thumbs/{data.exerciseThumbnailUri}"/>
									</mx:VBox>
								</mx:Component>
							</mx:itemRenderer>
						</mx:DataGridColumn>
						<mx:DataGridColumn headerText="{resourceManager.getString('myResources', 'OWNER')}"
										   dataField="responseUserName"/>
						<mx:DataGridColumn headerText="{resourceManager.getString('myResources', 'IRAUPENA')}"
										   dataField="responseDuration"
										   labelFunction="{durationLabelFunction}"/>
						<mx:DataGridColumn headerText="{resourceManager.getString('myResources', 'BALORAZIOKOP')}"
										   dataField="responseRatingAmount"/>
						<mx:DataGridColumn headerText="{resourceManager.getString('myResources', 'SELECTEDCHAR')}"
										   dataField="responseCharacterName"
										   visible="false"/>
						<mx:DataGridColumn headerText="{resourceManager.getString('myResources', 'DATE')}"
										   dataField="responseAddingDate"/>
					</mx:columns>
				</mx:DataGrid>

				<!-- Epaitu gabeko bideoak baloratzeko kaxa -->
				<mx:HBox paddingLeft="10"
						 paddingRight="10"
						 paddingBottom="10"
						 paddingTop="10"
						 width="100%">
					<mx:VBox id="ratingBox"
							 width="100%"
							 backgroundColor="#000000"
							 backgroundAlpha="0.13"
							 horizontalAlign="center"
							 verticalAlign="middle"
							 paddingRight="4"
							 paddingLeft="4"
							 paddingTop="4"
							 paddingBottom="4"
							 visible="false"
							 borderStyle="solid">
						<mx:RadioButtonGroup id="ratingRadioGroup"/>
						<mx:Label text="{resourceManager.getString('myResources','MESSAGE_RATE_THE_USERS_RESPONSE')}"
								  fontWeight="bold"/>
						<mx:HRule width="100%"/>
						<mx:HBox>
							<mx:RadioButton id="bat"
											group="{ratingRadioGroup}"
											label="1"
											value="1"/>
							<mx:RadioButton id="bi"
											group="{ratingRadioGroup}"
											label="2"
											value="2"/>
							<mx:RadioButton id="hiru"
											group="{ratingRadioGroup}"
											label="3"
											value="3"/>
							<mx:RadioButton id="lau"
											group="{ratingRadioGroup}"
											label="4"
											value="4"/>
							<mx:RadioButton id="bost"
											group="{ratingRadioGroup}"
											label="5"
											value="5"/>
						</mx:HBox>

						<mx:Button label="{resourceManager.getString('myResources', 'ADDVIDEO')}"
								   click="addVideoComment()"
								   id="addVideoCommentButton"/>
						<mx:Label id="addVideoCommentLabel"
								  text="{resourceManager.getString('myResources','MESSAGE_ADD_VIDEO_EVALUATION')}"
								  visible="false"/>
						<mx:CheckBox id="videoCommentCheckBox"
									 visible="false"/>

						<mx:TextArea width="100%"
									 height="82"
									 id="commentsTextArea"/>
						<mx:HBox>
							<mx:Button label="{resourceManager.getString('myResources', 'BIDALI')}"
									   click="sendAssessmentClickHandler()"
									   id="sendAssessmentButton"
									   enabled="false"/>
							<mx:Button label="{resourceManager.getString('myResources', 'RESET')}"
									   click="reset()"/>
						</mx:HBox>
					</mx:VBox>
				</mx:HBox>
			</mx:VBox>

			<!-- Niri epaitutako bideoen zerrenda erakusten du -->
			<mx:VBox id="assessedToCurrentUserBox">
				<mx:DataGrid width="100%"
							 height="100%"
							 id="assessedToCurrentUserDataGrid"
							 horizontalScrollPolicy="off"
							 verticalScrollPolicy="off"
							 verticalAlign="middle"
							 dataProvider="{assessedToCurrentUserList}"
							 change="assessedToCurrentUserChangeHandler(event)">
					<mx:columns>
						<mx:DataGridColumn headerText="{resourceManager.getString('myResources', 'JATORRIZKOA')}"
										   dataField="exerciseThumbnailUri"
										   width="{120*0.8}">
							<mx:itemRenderer>
								<mx:Component>
									<mx:VBox>
										<mx:Image id="albumImageNorb"
												  height="{90*0.8}"
												  width="{120*0.8}"
												  source="resources/images/thumbs/{data.exerciseThumbnailUri}"/>
									</mx:VBox>
								</mx:Component>
							</mx:itemRenderer>
						</mx:DataGridColumn>
						<mx:DataGridColumn headerText="{resourceManager.getString('myResources', 'IRAUPENA')}"
										   dataField="exerciseDuration"
										   labelFunction="{durationLabelFunction}"/>
						<mx:DataGridColumn headerText="{resourceManager.getString('myResources', 'BALORAZIOKOP')}"
										   dataField="responseRatingAmount"/>
						<mx:DataGridColumn headerText="{resourceManager.getString('myResources', 'AVG')}"
										   dataField="evaluationAverage"/>
						<mx:DataGridColumn headerText="{resourceManager.getString('myResources', 'SELECTEDCHAR')}"
										   dataField="responseCharacterName"/>
					</mx:columns>
				</mx:DataGrid>
				<mx:Spacer height="30"/>
				<!-- Niri epaitutako bideoen xehetasunak ematen dituen panela -->
				<mx:HBox id="assessmentsOfSelectedResponse"
						 width="100%"
						 height="100%">
					<mx:TabNavigator id="evaluationTypeNavigator"
									 creationPolicy="all"
									 width="100%"
									 paddingLeft="8"
									 paddingRight="8"
									 paddingTop="8"
									 paddingBottom="8">
						<mx:VBox id="humanEvaluation"
								 label="Human Evaluation">
							<mx:HBox>
								<mx:Label id="averageRating"/>
								<mx:Button id="viewGraphicButton"
										   label="{resourceManager.getString('myResources', 'DISPLAYGRAPH')}"
										   click="viewGraphicClickHandler()"/>
								<!-- <mx:Image source="images/graf_icon.png" width="20" height="20" click="grafikoaIkusi()"/> -->
							</mx:HBox>

							<mx:DataGrid id="assessmentDetailDataGrid"
										 dataProvider="{assessmentDetailList}"
										 change="assessmentDetailChangeHandler(event)"
										 verticalAlign="middle"
										 width="100%">
								<mx:columns>
									<mx:DataGridColumn headerText="{resourceManager.getString('myResources', 'JUDGE')}"
													   dataField="userName"/>
									<mx:DataGridColumn headerText="{resourceManager.getString('myResources', 'VALUATION')}"
													   dataField="score"/>
									<mx:DataGridColumn headerText="{resourceManager.getString('myResources', 'DATE')}"
													   dataField="addingDate"/>
									<mx:DataGridColumn headerText="{resourceManager.getString('myResources', 'COMMENTS')}"
													   dataField="comment"
													   width="160">
										<mx:itemRenderer>
											<mx:Component>
												<mx:Text text="{data.comment}"
														 width="160"/>
											</mx:Component>
										</mx:itemRenderer>
									</mx:DataGridColumn>
									<mx:DataGridColumn headerText="{resourceManager.getString('myResources', 'VIDEOCOMMENT')}"
													   dataField="evaluationVideoFileIdentifier"/>
								</mx:columns>
							</mx:DataGrid>
						</mx:VBox>
						<ns2:AutoevaluationPanel id="computerEvaluation">
						</ns2:AutoevaluationPanel>
					</mx:TabNavigator>
				</mx:HBox>

			</mx:VBox>


			<!-- Nik epaitu ditudan bideoen zerrenda erakusten du -->
			<mx:VBox label="{resourceManager.getString('myResources', 'NIKEPAITUTAKOAK')}"
					 id="assessedByCurrentUserBox">
				<mx:DataGrid width="100%"
							 id="assessedByCurrentUserDataGrid"
							 dataProvider="{assessedByCurrentUserList}"
							 change="assessedByCurrentUserChangeHandler(event)"
							 verticalAlign="middle"
							 verticalScrollPolicy="off">
					<mx:columns>
						<mx:DataGridColumn headerText="{resourceManager.getString('myResources', 'JATORRIZKOA')}"
										   dataField="exerciseThumbnailUri"
										   width="{120*0.8}">
							<mx:itemRenderer>
								<mx:Component>
									<mx:VBox>
										<mx:Image id="albumImageNorb"
												  height="{90*0.8}"
												  width="{120*0.8}"
												  source="resources/images/thumbs/{data.exerciseThumbnailUri}"/>
									</mx:VBox>
								</mx:Component>
							</mx:itemRenderer>
						</mx:DataGridColumn>
						<mx:DataGridColumn headerText="{resourceManager.getString('myResources', 'GRABADOR')}"
										   dataField="responseUserName"/>
						<mx:DataGridColumn headerText="{resourceManager.getString('myResources', 'IRAUPENA')}"
										   dataField="responseDuration"
										   labelFunction="{durationLabelFunction}"/>
						<mx:DataGridColumn headerText="{resourceManager.getString('myResources', 'BALORAZIOKOP')}"
										   dataField="responseRatingAmount"/>
						<mx:DataGridColumn headerText="{resourceManager.getString('myResources', 'SELECTEDCHAR')}"
										   dataField="responseCharacterName"/>
					</mx:columns>
				</mx:DataGrid>
			</mx:VBox>
		</mx:ViewStack>
	</mx:VBox>


</mx:HBox>
