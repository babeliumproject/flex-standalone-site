<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" 
		 xmlns:view="modules.assessment.view.*" 
		 xmlns:videoPlayer="components.videoPlayer.*"
		 paddingTop="16"
		 paddingBottom="16"
		 paddingLeft="16"
		 paddingRight="16"
		 implements="modules.IGroupInterface"
		 creationComplete="assessdetail_creationCompleteHandler(event)">
	
	<fx:Script>
		<![CDATA[
			import model.DataModel;
			
			import modules.subtitle.event.SubtitleEvent;
			
			import mx.binding.utils.BindingUtils;
			import mx.collections.ArrayCollection;
			import mx.events.FlexEvent;
			import mx.resources.ResourceManager;
			import mx.utils.ArrayUtil;
			import mx.utils.ObjectUtil;
			
			import vo.EvaluationVO;
			
			[Bindable]
			protected var _exerciseTitle:String;
			
			[Bindable]
			private var dataModel:DataModel=DataModel.getInstance();
			
			protected var _currentCaptions:Object;
			protected var _currentTimeMarkers:Object;
			
			protected var _leftMedia:Object;
			protected var _rightMedia:Object;
			
			protected var _selectedRole:String;
			protected var _subtitleId:int;
			protected var _rolesReady:Boolean;
			
			protected var assessmentCollection:ArrayCollection;
			
			public function resetGroup():void{
				VP.resetComponent();
				_currentCaptions=_currentTimeMarkers=_leftMedia=_rightMedia=null;
				_subtitleId=0;
				_selectedRole=null;
				_rolesReady=false;
				assessmentsOfSubmission.removeAllElements();
				assessmentCollection=null;
			}
			
			protected function submissionAssessmentsHandler(value:Boolean):void{
				var data:Object=dataModel.responseAssessmentData;
				if(data){
					_selectedRole=data.character_name;
					_subtitleId=data.fk_subtitle_id;
					if(data.hasOwnProperty('leftMedia')){
						_leftMedia=data.leftMedia;
					}
					if(data.hasOwnProperty('rightMedia')){
						_rightMedia=data.rightMedia;
					}
					if(data.hasOwnProperty('assessments')){
						assessmentCollection = data.assessments ? new ArrayCollection(ArrayUtil.toArray(data.assessments)) : null;
						if(assessmentCollection){
							noresultLbl.includeInLayout=false;
							noresultLbl.visible=false;
							assessmentsOfSubmission.includeInLayout=true;
							assessmentsOfSubmission.visible=true;		
						} else {
							noresultLbl.includeInLayout=true;
							noresultLbl.visible=true;
							assessmentsOfSubmission.includeInLayout=false;
							assessmentsOfSubmission.visible=false;
						}
					} else {
						assessmentCollection = null;
					}
					buildEvaluationDatalPanel();
					fetchSubtitlesById(_subtitleId);
				}
			}
			
			protected function fetchSubtitlesById(subtitleid:int):void{
				var params:Object=new Object();
				params.id=subtitleid;
				new SubtitleEvent(SubtitleEvent.GET_EXERCISE_SUBLINES, params).dispatch();
			}
			
			protected function onSubtitlesRetrieved(value:Boolean):void{
				_currentCaptions=dataModel.availableSubtitleLines;
				VP.setCaptions(_currentCaptions);
			}
			
			protected function onRolesRetrieved(value:Boolean):void
			{
				var _roles:Object = dataModel.availableExerciseRoles;
				if(_roles && _roles.hasOwnProperty(_selectedRole)){
					_currentTimeMarkers = _roles[_selectedRole];
					_rolesReady=true;
					prepareVideoPlayer();
				} else {
					_currentTimeMarkers=null;
					_rolesReady=false;
				}
			}
			
			protected function prepareVideoPlayer():void{
				var parallelMedia:Object=new Object();
				parallelMedia.leftMedia=_leftMedia;
				parallelMedia.rightMedia=_rightMedia;
				VP.loadVideoByUrl(parallelMedia, _currentTimeMarkers);
			}
			
			protected function buildEvaluationDatalPanel():void{
				assessmentsOfSubmission.removeAllElements();
				if(assessmentCollection){
					for each (var e:EvaluationVO in assessmentCollection)
					{
						var edb:EvaluationDetailBox=new EvaluationDetailBox();
						assessmentsOfSubmission.addElement(edb);
						edb.evaluationData=e;
					}
				}
			}
			
			protected function assessdetail_creationCompleteHandler(event:FlexEvent):void
			{
				removeEventListener(FlexEvent.CREATION_COMPLETE, assessdetail_creationCompleteHandler);
				var commitOnly:Boolean = false;
				var useWeakReference:Boolean = true;
				BindingUtils.bindSetter(submissionAssessmentsHandler, dataModel, "responseAssessmentDataRetrieved", commitOnly, useWeakReference);
				BindingUtils.bindSetter(onRolesRetrieved, dataModel, "availableExerciseRolesRetrieved", commitOnly, useWeakReference);
				BindingUtils.bindSetter(onSubtitlesRetrieved, dataModel, "availableSubtitleLinesRetrieved", commitOnly, useWeakReference);
			}
			
		]]>
	</fx:Script>
	
	<s:VGroup id="videoPlayerBox"
			  width="100%"
			  horizontalAlign="center"
			  paddingTop="20"
			  paddingLeft="16"
			  paddingRight="16">
		<!--
		<s:Label id="exerciseTitle"
				 text="{_exerciseTitle}"
				 width="100%"
				 styleName="exerciseTitleLabel"/>
		-->
		<s:HGroup width="100%"
				  horizontalAlign="center">
			<videoPlayer:VideoRecorder id="VP"
									   autoPlay="true"
									   skinUrl="/resources/videoPlayer/skin/white.xml"
									   seek="false"
									   displayCaptions="true"
									   autoScale="false"
									   subtitlingControls="false"
									   width="640"
									   height="360"/>
		</s:HGroup>
	</s:VGroup>
	
	<s:Label id="noresultLbl" 
			 text="{ResourceManager.getInstance().getString('myResources','NO_RESULTS_WERE_RETURNED_FOR_THAT_QUERY')+'.'}"/>
	
	<s:VGroup id="assessmentsOfSubmission" width="100%"/>
	
</s:VGroup>
