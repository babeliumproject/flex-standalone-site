<?xml version="1.0" encoding="utf-8"?>
<s:Module xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  xmlns:mx="library://ns.adobe.com/flex/mx"
		  xmlns:view="modules.login.view.*"
		  width="100%"
		  implements="modules.IModuleInterface" 
		  preinitialize="preinitializeHandler(event)"
		  creationComplete="creationCompleteHandler(event)">
	<s:layout>
		<s:VerticalLayout/>
	</s:layout>
	
	<fx:Script>
		<![CDATA[
			import control.URLChangeEvent;
			import control.URLManager;
			
			import events.RegisterUserEvent;
			
			import model.DataModel;
			
			import modules.IGroupInterface;
			import modules.IPinnedGroupInterface;
			
			import mx.binding.utils.BindingUtils;
			import mx.binding.utils.ChangeWatcher;
			import mx.events.FlexEvent;
			import mx.utils.ObjectUtil;
			import mx.utils.URLUtil;
			
			import spark.components.NavigatorContent;
			
			import vo.NewUserVO;
			
			public static const INDEX:uint=0;
			public static const ACTIVATE:uint=1;
			public static const FORGOTPASSWORD:uint=2;
			
			public var modulename:String = 'login';
			
			private var _viewstackIndex:uint;
			
			private var _creationComplete:Boolean;
			private var _initialUrl:Event;
			private var _moduleController:*;
			
			private var _urlmanager:URLManager=URLManager.getInstance();
			private var _model:DataModel=DataModel.getInstance();	
			
			public function getModuleName():String{
				return modulename;
			}
			
			public function setDataBindings(data:* = null):void{
				_moduleController = new Object();	
				if(data){
					if(data['initialurl']){
						_initialUrl=data['initialurl'];
					}
				}
			}
			
			public function resetModule():void{
				_urlmanager.removeEventListener(URLChangeEvent.CHANGE, onURLChange);
				_urlmanager=null;
				
				this.removeEventListener(FlexEvent.PREINITIALIZE, preinitializeHandler);
				this.removeEventListener(FlexEvent.CREATION_COMPLETE, creationCompleteHandler);
				this.removeEventListener(FlexEvent.ADD, componentAddedHandler);
				this.removeEventListener(FlexEvent.UPDATE_COMPLETE, componentUpdateCompleteHandler);
				
				_moduleController = null;
				_model = null;
				resetViews();
				unpinViews();
			}
			
			public function resetViews():void{
				if (moduleViewstack)
				{
					var views:Array=moduleViewstack.getChildren();
					for each (var nc:NavigatorContent in views)
					{
						if (nc.deferredContentCreated)
						{
							var v:*=nc.getElementAt(0) as IPinnedGroupInterface;
							if (v)
								v.resetGroup();
						}
					}
				}
			}
			
			public function unpinViews():void{
				if (moduleViewstack)
				{
					var views:Array=moduleViewstack.getChildren();
					for each (var nc:NavigatorContent in views)
					{
						if (nc.deferredContentCreated)
						{
							var v:*=nc.getElementAt(0) as IPinnedGroupInterface;
							if (v)
								v.unpinGroup();
						}
					}
				}
			}
			
			public function onURLChange(event:Event):void{
				
		
				if(!event) return;
				
				var evt:Object=Object(event);
				
				if(!evt.module || evt.module != modulename) return;
				
				//Reset the components before changing the foreground
				resetViews();
				
				switch (evt.action){
					case 'activate':
						actionActivate(evt.parameters);
						break;
					case 'forgotpassword':
						actionForgotpassword();
						break;
					default:
						actionIndex();
						break;
				}
			}
			
			public function actionIndex():void{
				_viewstackIndex = INDEX;
				moduleViewstack.selectedIndex=_viewstackIndex;
			}
			
			public function actionActivate(parameters:Object):void{
				_viewstackIndex = ACTIVATE;
				moduleViewstack.selectedIndex=_viewstackIndex;
				if (parameters != null)
				{
					var user:String,hash:String;
					//Links coming from email clients might be escaped. Unescape to make sure
					if(!parameters.hasOwnProperty('hash') || !parameters.hasOwnProperty('user')){
						var unhexparams:String=unescape(parameters.id);
						var uparams:Object=URLUtil.stringToObject(unhexparams,'&',true);
						user=uparams.user || null;
						hash=uparams.hash || null;
					} else {
						user=parameters.user;
						hash=parameters.hash;
					}
					
					if (hash != '' && user != '')
					{
						var data:Object = new Object();
						data.username=user;
						data.activation_hash=hash;
						new RegisterUserEvent(RegisterUserEvent.ACTIVATE_USER, data).dispatch();
					}
				}
			}
			
			public function actionForgotpassword():void{
				_viewstackIndex = FORGOTPASSWORD;
				moduleViewstack.selectedIndex=_viewstackIndex;
			}
			
			protected function preinitializeHandler(event:FlexEvent):void
			{
				//if(!_model.moduleMap.hasOwnProperty(modulename))
				//	_model.moduleMap[modulename] = new CourseModel();
				
				// The Event.ADDED and Event.ADDED_TO_STAGE events are dispatched before FlexEvent.PREINITIALIZE
				// that leaves only FlexEvent.ADD to know the component has been added as a child somewhere.
				this.addEventListener(FlexEvent.ADD, componentAddedHandler, false, 0, true);
				this.addEventListener(FlexEvent.UPDATE_COMPLETE, componentUpdateCompleteHandler, false, 0, true);
			}
			
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				_creationComplete=true;
				_urlmanager.addEventListener(URLChangeEvent.CHANGE, onURLChange, false, 0, true);
				onURLChange(_initialUrl);
			}
			
			protected function componentAddedHandler(event:FlexEvent):void{
				this.visible=false;
			}
			
			protected function componentUpdateCompleteHandler(event:FlexEvent):void{
				checkPermissions();
			}
			
			protected function checkPermissions():void{
				if (DataModel.getInstance().isLoggedIn)
				{
					URLManager.getInstance().redirect('/exercises');
					return;
				}
				this.visible=true;
			}
			
		]]>
	</fx:Script>
	
	<mx:ViewStack id="moduleViewstack"
				  resizeToContent="true"
				  width="100%"
				  height="100%">
		<s:NavigatorContent id="index">
			<view:LoginIndex id="loginIndex"/>
		</s:NavigatorContent>
		<s:NavigatorContent id="activate">
			<view:LoginActivate id="loginActivate"/>
		</s:NavigatorContent>
		<s:NavigatorContent id="forgotpassword">
			<view:LoginForgotpassword id="loginForgotpassword"/>
		</s:NavigatorContent>
	</mx:ViewStack>
</s:Module>
