<mx:HBox xmlns:mx="http://www.adobe.com/2006/mxml"
		 width="100%"
		 height="100%"
		 xmlns:ns1="modules.exercises.*"
		 xmlns:ns2="view.*"
		 xmlns:ns3="view.VideoPlayer.*"
		 xmlns:ns4="modules.videoUpload.*"
		 xmlns:extenders="modules.extenders.*"
		 backgroundAlpha="0.0"
		 creationComplete="init()"
		 xmlns:local="*" xmlns:videoPlayer="modules.videoPlayer.*">
	<mx:Metadata>
		[ResourceBundle("myResources")]
	</mx:Metadata>
	<mx:Script>
		<![CDATA[
			import control.CuePointManager;
			
			import events.SubtitlesAndRolesEvent;
			import events.SubtitlesEvent;
			
			import flash.utils.*;
			
			import model.DataModel;
			
			import modules.videoPlayer.events.VideoPlayerEvent;
			import modules.videoPlayer.events.babelia.StreamEvent;
			import modules.videoPlayer.events.babelia.SubtitlingEvent;
			
			import mx.collections.*;
			import mx.controls.Alert;
			import mx.controls.Label;
			import mx.controls.dataGridClasses.DataGridColumn;
			import mx.events.CloseEvent;
			
			import view.*;
			
			import vo.CreditHistoryVO;
			import vo.ExerciseVO;
			import vo.SubtitleAndSubtitleLinesVO;
			import vo.SubtitlesAndRolesVO;

			[Embed(source='../../resources/images/playerPlayIcon.png')]
			[Bindable]
			private var playImageEmbed:Class;
			[Embed(source='../../resources/images/playerPauseIcon.png')]
			[Bindable]
			private var pauseImageEmbed:Class;


			[Bindable]
			[Embed(source="../../resources/images/flags/flag_united_kingdom.png")]
			public var FlagEnglish:Class;

			[Bindable]
			[Embed(source="../../resources/images/flags/flag_spain.png")]
			public var FlagSpanish:Class;

			[Bindable]
			[Embed(source="../../resources/images/flags/flag_france.png")]
			public var FlagFrench:Class;

			[Bindable]
			[Embed(source="../../resources/images/flags/flag_basque_country.png")]
			public var FlagBasque:Class;

			[Bindable]
			private var flaggedLanguageData:Array=new Array({label: 'English', icon: 'FlagEnglish'}, 
															{label: 'Spanish', icon: 'FlagSpanish'}, 
															{label: 'Basque', icon: 'FlagBasque'}, 
															{label: 'French', icon: 'FlagFrench'});

			/**
			 * CONTROL
			 **/
			private var cueManager:CuePointManager = CuePointManager.getInstance();
			
			private var videoPlayerReady:Boolean = false;
			private var availableRoles:ArrayCollection;
			
			private var progressUpdateTimer:Timer=null;

			[Bindable] private var videoHeight:int = 250;
			[Bindable] private var videoWidth:int = 400;
			[Bindable] private var streamSource:String = "rtmp://" + DataModel.getInstance().server + "/oflaDemo";

			private var volumeTransform:SoundTransform;

			private var exerciseFileName:String;
			private var exerciseId:int;
			private var exerciseLanguage:String;

			[Bindable]
			private var videoPlayerControlsViewStack:int=1;
			
			public function init():void
			{
				if (DataModel.getInstance().currentExercise != null)
				{
					var watchExercise:ExerciseVO=DataModel.getInstance().currentExercise;
					setBideoIzena(watchExercise.name, watchExercise.id);
					setupVideoPlayer();
					exerciseTitle.text=watchExercise.title;
				}
				else
				{
					setBideoIzena("kutsi9", 1);
				}
			}

			private function resolveIdToRole(item:Object, column:DataGridColumn):String
			{
				for each (var dp:Object in comboData)
				{
					if (dp.roleId == item.roleId)
						return dp.charName;
				}
				return "";
			}

			public function setBideoIzena(exerciseFileName:String, exerciseId:int):void
			{
				this.exerciseFileName=exerciseFileName;
				this.exerciseId=exerciseId;
			}
			
			public function setupVideoPlayer() : void
			{
				VP.addEventListener(VideoPlayerEvent.CONNECTED, onVideoPlayerReady);
				VP.setLocales(new ArrayCollection(DataModel.getInstance().locales));
				VP.addEventListener(SubtitlingEvent.START, subtitleStartHandler);
				VP.addEventListener(SubtitlingEvent.END, subtitleEndHandler);
			}
			
			public function onVideoPlayerReady(e:VideoPlayerEvent) : void
			{
				videoPlayerReady = true;
				VP.videoSource = exerciseFileName;
				VP.addEventListener(StreamEvent.ENTER_FRAME, monitorCuePoints);
				VP.enableSubtitlingEndButton = false;
			}

			private function set onExerciseSelected(value:Boolean):void
			{
				if (DataModel.getInstance().currentExerciseRetrieved == true)
				{
					DataModel.getInstance().currentExerciseRetrieved=false;
					var watchExercise:ExerciseVO=DataModel.getInstance().currentExercise;
					exerciseTitle.text=watchExercise.title;
					exerciseFileName=watchExercise.name;
					exerciseId=watchExercise.id;
					exerciseLanguage=watchExercise.language;

					//When requested to watch this exercise we also asked to grab the subtitles it may have.
					//In case it has subtitles, we populate our subtitleCollection with them.
					if (DataModel.getInstance().availableSubtitlesAndRoles != null)
					{
						subtitleCollection=DataModel.getInstance().availableSubtitlesAndRoles;
					}

					if ( videoPlayerReady )
					{
						VP.videoSource = watchExercise.name;
						VP.removeEventListener(StreamEvent.ENTER_FRAME, monitorCuePoints);
						VP.addEventListener(StreamEvent.ENTER_FRAME, monitorCuePoints);
						VP.enableSubtitlingEndButton = false;	
					}
				}
			}


			//
			// SUBTITLE ADDITION ALPHA EDITION
			// ALSO MUST CHECK WHETHER IT'S POSSIBLE TO USE NON-ENBEDDED CUEPOINTS IN EXERCISE MODULE
			//

			[Bindable]
			private var isVideoPlaying:Boolean=false;

			[Bindable]
			private var subtitleStartTime:Number=0;
			[Bindable]
			private var subtitleEndTime:Number=0;
			[Bindable]
			private var subtitleLength:Number=0;
			[Bindable]
			private var subtitleCollection:ArrayCollection=new ArrayCollection();
			private var startEntry:SubtitlesAndRolesVO;
			private var endEntry:SubtitlesAndRolesVO;
			private var detailEntry:SubtitlesAndRolesVO;

			private var preUpdateFinished:Boolean=true;

			[Bindable]
			private var outlinedSubtitle:TextOutlines=new TextOutlines(320, 50, 1.8);
			
			private var subtitleEditorVisible:Boolean=false;


			public function set onSubtitlesAndRolesRetrieved(value:Boolean):void
			{
				if (DataModel.getInstance().availableSubtitlesAndRolesRetrieved)
				{
					DataModel.getInstance().availableSubtitlesAndRolesRetrieved=false;
					subtitleCollection=DataModel.getInstance().availableSubtitlesAndRoles;
					subtitleList.dataProvider=subtitleCollection;
				}
			}

			private function subtitleStartHandler(e:SubtitlingEvent):void
			{
				VP.enableSubtitlingEndButton = true;
				subtitleStartTime=e.time;
				startEntry=new SubtitlesAndRolesVO();
				startEntry.id=subtitleCollection.length + 1;
				startEntry.showTime=subtitleStartTime;
				startEntry.hideTime=subtitleStartTime + 0.5;
				startEntry.role="";
				startEntry.text="";
				startEntry.subtitleId=0;
				subtitleCollection.addItem(startEntry);

				var showTimeSort:SortField=new SortField();
				showTimeSort.name="showTime";
				showTimeSort.numeric=true;
				var numericDataSort:Sort=new Sort();
				numericDataSort.fields=[showTimeSort];
				subtitleCollection.sort=numericDataSort;
				subtitleCollection.refresh();

				insertDataGridRow();
			}

			private function subtitleEndHandler(e:SubtitlingEvent):void
			{
				VP.enableSubtitlingEndButton = false;

				if (subtitleCollection.length > 0)
				{
					subtitleEndTime=e.time;
					endEntry=new SubtitlesAndRolesVO();
					endEntry.id=subtitleCollection.length;
					endEntry.showTime=subtitleStartTime;
					endEntry.hideTime=subtitleEndTime;
					endEntry.role="";
					endEntry.text="";
					endEntry.subtitleId=0;

					subtitleCollection.setItemAt(endEntry, subtitleCollection.getItemIndex(startEntry));
					insertDataGridRow();
				}
			}

			private function subtitleInsertHandler():void
			{

			}

			private function subtitleRemoveHandler():void
			{
				if (subtitleList.selectedIndex != -1)
				{

					var previouslySelectedIndex:Number=subtitleList.selectedIndex;
					var indexToBeSelected:Number;
					if (previouslySelectedIndex == subtitleList.rowCount)
					{
						indexToBeSelected=previouslySelectedIndex - 1;
					}
					else if (previouslySelectedIndex == 0 && subtitleList.rowCount == 1)
					{
						//nothing
					}
					else
					{
						indexToBeSelected=previouslySelectedIndex;
					}
					subtitleCollection.removeItemAt(subtitleList.selectedIndex);
					insertDataGridRow();
					subtitleList.selectedIndex=indexToBeSelected;

				}
			}

			private function subtitleClearHandler():void
			{
				Alert.show(resourceManager.getString('myResources','WARNING_CLEAR_SUBTITLE_LINES'), 
						   resourceManager.getString('myResources','TITLE_CONFIRM_ACTION'), 
						   Alert.YES | Alert.NO, null, subtitleClearConfirmation, null, Alert.NO);
			}

			private function subtitleClearConfirmation(event:CloseEvent):void
			{
				if (event.detail == Alert.YES)
					subtitleCollection.removeAll();
			}

			private function subtitleNextHandler():void
			{
				var currentlySelectedIndex:Number=subtitleList.selectedIndex;
				if (currentlySelectedIndex != -1 && currentlySelectedIndex < subtitleList.rowCount)
				{
					subtitleList.selectedIndex=currentlySelectedIndex + 1;
				}
			}

			private function subtitlePreviousHandler():void
			{
				var currentlySelectedIndex:Number=subtitleList.selectedIndex;
				if (currentlySelectedIndex != -1 && currentlySelectedIndex > 0)
				{
					subtitleList.selectedIndex=currentlySelectedIndex - 1;
				}
			}

			private function insertDataGridRow():void
			{
				subtitleList.dataProvider=subtitleCollection;
			}

			private function goToTimeHandler():void
			{
				if (subtitleList.selectedIndex != -1)
				{
					var tempEntry:SubtitlesAndRolesVO=subtitleCollection.getItemAt(subtitleList.selectedIndex) as SubtitlesAndRolesVO;
					//ns.pause();
					//VP.seekTo(tempEntry.showTime);
						//ns.resume();
				}
			}

			private function monitorCuePoints(e:StreamEvent):void
			{
				var curTime:Number=e.time;
				for each (var ob1:SubtitlesAndRolesVO in subtitleCollection)
				{
					if ((curTime - 0.08) < ob1.showTime && ob1.showTime < (curTime + 0.08))
					{
						doShowCuePoint(ob1);
						break;
					}
				}
				for each (var ob2:SubtitlesAndRolesVO in subtitleCollection)
				{
					if ((curTime - 0.08) < ob2.hideTime && ob2.hideTime < (curTime + 0.08))
					{
						doHideCuePoint(ob2);
						break;
					}
				}
			}

			private function doShowCuePoint(obj:SubtitlesAndRolesVO):void
			{
				VP.setSubtitle(obj.text);
			}

			private function doHideCuePoint(obj:SubtitlesAndRolesVO):void
			{
				VP.setSubtitle("");
			}

			private function viewSubtitlingControls(event:MouseEvent):void
			{
				if( !VP.subtitlingControls ){
					videoPlayerControlsViewStack=0;
					VP.subtitlingControls = true;
				} else {
					videoPlayerControlsViewStack=1;
					VP.subtitlingControls = false;
				}
			}
			
			private function hideSubtitlingControls(event:MouseEvent):void{
				// TODO hide subtitle controls
			}

			private function saveSubtitlesHandler():void
			{
				if (subtitleCollection.length > 0)
				{
					var errors:String=checkSubtitleErrors();
					if (errors.length == 0)
					{
						for each (var s:SubtitlesAndRolesVO in subtitleCollection)
						{
							for each (var dp:Object in comboData)
							{
								if (dp.roleId == s.roleId)
								{
									s.singleName=dp.charName;
									s.role=dp.charName;
								}
							}
						}
						var subtitleLines:Array=subtitleCollection.toArray();
						var subtitles:SubtitleAndSubtitleLinesVO=new SubtitleAndSubtitleLinesVO();
						subtitles.exerciseId=exerciseId;
						subtitles.userId=DataModel.getInstance().loggedUser.id;
						subtitles.language=languageComboBox.selectedLabel;
						subtitles.subtitleLines=subtitleLines;
						if (DataModel.getInstance().availableSubtitlesAndRoles.length == 0)
							subtitles.id=0;
						else
							subtitles.id=DataModel.getInstance().availableSubtitlesAndRoles.getItemAt(0).subtitleId;

						var subHistoricData:CreditHistoryVO=new CreditHistoryVO();
						subHistoricData.videoExerciseId=exerciseId;
						subHistoricData.videoExerciseName=exerciseFileName;
						DataModel.getInstance().subHistoricData=subHistoricData;

						new SubtitlesEvent(SubtitlesEvent.SAVE_SUBTITLES, subtitles).dispatch();
					}
					else
					{
						Alert.show(errors,resourceManager.getString('myResources','WARNING_SUBTITLE_HAS_ERRORS'));
					}
				}
				else
				{
					Alert.show(resourceManager.getString('myResources','WARNING_EMPTY_SUBTITLE'),
							   resourceManager.getString('myResources','TITLE_INFORMATION_MESSAGE'));
				}

			}

			private function checkSubtitleErrors():String
			{
				var errorMessage:String="";
				//Check empty roles, time overlappings and empty texts
				for (var i:int=0; i < subtitleCollection.length; i++)
				{
					if (subtitleCollection.getItemAt(i).roleId < 1)
						errorMessage+="The role on the line " + (i + 1) + " is empty.\n";
					var lineText:String=subtitleCollection.getItemAt(i).text;
					lineText=lineText.replace(/[ ,\;.\:\-_?¿¡!€$']*/, "");
					if (lineText.length < 1)
						errorMessage+="The text on the line " + (i + 1) + " is empty.\n";
					if (i > 0)
					{
						if (subtitleCollection.getItemAt((i-1)).hideTime >= subtitleCollection.getItemAt(i).showTime)
							errorMessage+="The subtitle on the line " + i + " overlaps with the next subtitle.\n";
					}
				}
				return errorMessage;
			}

			private function set onSubtitleSaved(value:Boolean):void
			{
				if (DataModel.getInstance().subtitleSaved)
				{
					DataModel.getInstance().subtitleSaved=false;
					var sarvo:SubtitlesAndRolesVO=new SubtitlesAndRolesVO();
					sarvo.exerciseId=exerciseId;
					sarvo.language=languageComboBox.selectedLabel;
					new SubtitlesAndRolesEvent(SubtitlesAndRolesEvent.GET_ROLES, sarvo).dispatch();
					new SubtitlesAndRolesEvent(SubtitlesAndRolesEvent.GET_INFO_SUB_ROLES, sarvo).dispatch();
				}

			}

			private function set availableRolesRetrieved(value:Boolean):void
			{

				if (DataModel.getInstance().availableExerciseRoles)
				{
					var avrol:ArrayCollection=DataModel.getInstance().availableExerciseRoles;
					var cData:ArrayCollection=new ArrayCollection;
					var insertLine:Object={roleId: 0, charName: resourceManager.getString('myResources','OPTION_INSERT_NEW_ROLE'), action: 'insert', fontWeight: 'bold', indent: 0};
					cData.addItem(insertLine);
					if (avrol.length > 0)
					{
						for each (var item:Object in avrol)
						{
							var comboline:Object={roleId: item.data, charName: item.label, action: 'select', fontWeight: 'normal', indent: 10};
							cData.addItem(comboline);
						}
						var deleteLine:Object={roleId: 0, charName: resourceManager.getString('myResources','OPTION_DELETE_A_ROLE'), action: 'nothing', fontWeight: 'bold', indent: 0};
						cData.addItem(deleteLine);
						for each (var item2:Object in avrol)
						{
							var comboline2:Object={roleId: item2.data, charName: item2.label, action: 'delete', fontWeight: 'normal', indent: 10};
							cData.addItem(comboline2);
						}
						comboData.removeAll();
						comboData=cData;
					}
					else
					{
						var deleteLineEmpty:Object={roleId: 0, charName: resourceManager.getString('myResources','OPTION_DELETE_A_ROLE'), action: 'nothing', fontWeight: 'bold', indent: 0};
						cData.addItem(deleteLineEmpty);
						comboData.removeAll();
						comboData=cData;
					}
				}
			}

			private function lfRowNum(oItem:Object, iCol:int):String
			{
				var iIndex:int=subtitleCollection.getItemIndex(oItem) + 1;
				return String(iIndex);
			}
			
			private function set onTabChange(value:Boolean) : void {
				VP.endVideo();
				VP.subtitlingControls = false;
				VP.removeEventListener(StreamEvent.ENTER_FRAME, monitorCuePoints);
				hideSubtitlingControls(null);
			}
			
			private function set onLogout(value:Boolean):void{
				if(DataModel.getInstance().isLoggedIn == false){
					onTabChange = false;
				}
			}

		]]>

	</mx:Script>

	<mx:ArrayCollection id="comboData"/>

	<mx:Binding source="DataModel.getInstance().availableExerciseRoles"
				destination="availableRolesRetrieved"/>
	<mx:Binding source="{DataModel.getInstance().currentExerciseRetrieved}"
				destination="this.onExerciseSelected"/>
	<mx:Binding source="{DataModel.getInstance().availableSubtitlesAndRolesRetrieved}"
				destination="this.onSubtitlesAndRolesRetrieved"/>
	<mx:Binding source="{DataModel.getInstance().subtitleSaved}"
				destination="this.onSubtitleSaved"/>
	<mx:Binding source="{DataModel.getInstance().stopVideoFlag}"
				destination="this.onTabChange"/>
	<mx:Binding source="{DataModel.getInstance().isLoggedIn}"
				destination="this.onLogout"/>

	<!-- Video Player -->
	<mx:VBox>
		<mx:Label id="exerciseTitle"
				text="ExerciseTitle"
				width="100%"
				fontSize="16"
				fontWeight="bold"/>
		<videoPlayer:VideoPlayerBabelia id="VP" autoPlay="true" skin="white"
				seek="true" subtitles="true" autoScale="false" arrows="false"
				subtitlingControls="false" width="400" height="{VP.width * 800 / 1280}"
				streamSource="{streamSource}" />
		<mx:HBox width="100%"
				 horizontalAlign="center"
				 verticalAlign="middle">
			<mx:Button id="subtitleExerciseButton"
					   label="{resourceManager.getString('myResources','OPTION_ADD_SUBTITLES_TO_EXERCISE')}"
					   fontWeight="normal"
					   labelPlacement="bottom"
					   click="viewSubtitlingControls(event)"
					   width="80"
					   height="80"
					   enabled="{DataModel.getInstance().isLoggedIn}"
					   toolTip="{resourceManager.getString('myResources','TOOLTIP_ADD_SUBTITLES_TO_EXERCISE')}">
				<mx:icon>@Embed(source='../../resources/images/addSubtitles.png')</mx:icon>
			</mx:Button>
		</mx:HBox>
	</mx:VBox>

	<!-- Subtitle datagrid -->
	<mx:ViewStack height="100%"
				  width="100%"
				  selectedIndex="{this.videoPlayerControlsViewStack}">

		<mx:Panel width="100%"
				  height="100%"
				  layout="vertical"
				  title="{resourceManager.getString('myResources','TITLE_SUBTITLE_EDITOR')}"
				  fontFamily="Arial"
				  fontSize="12"
				  horizontalAlign="center"
				  verticalAlign="middle"
				  visible="{DataModel.getInstance().isLoggedIn}">
			<mx:HBox horizontalAlign="center"
					 verticalAlign="middle"
					 width="100%">
				<mx:Label text="{resourceManager.getString('myResources','MESSAGE_CHOOSE_SUBTITLE_LANGUAGE')}"/>
				<ns4:IconComboBox id="languageComboBox"
								  dataProvider="{flaggedLanguageData}"
								  fontWeight="normal"/>
			</mx:HBox>

			<mx:DataGrid id="subtitleList"
						 sortableColumns="false"
						 width="100%"
						 height="70%"
						 doubleClick="goToTimeHandler()"
						 doubleClickEnabled="true"
						 editable="true"
						 dataProvider="{this.subtitleCollection}">
				<mx:columns>
					<mx:DataGridColumn headerText="{resourceManager.getString('myResources','COLUMNHEAD_NUMBER')}"
									   editable="false"
									   labelFunction="{lfRowNum}"
									   width="60"/>
					<mx:DataGridColumn headerText="{resourceManager.getString('myResources','COLUMNHEAD_SHOW')}"
									   editable="true"
									   editorDataField="value"
									   width="85"
									   rendererIsEditor="true"
									   itemRenderer="modules.subtitles.ShowNumericStepperRenderer">
					</mx:DataGridColumn>
					<mx:DataGridColumn headerText="{resourceManager.getString('myResources','COLUMNHEAD_HIDE')}"
									   width="85"
									   editable="true"
									   editorDataField="value"
									   rendererIsEditor="true"
									   itemRenderer="modules.subtitles.HideNumericStepperRenderer"/>
					<mx:DataGridColumn headerText="{resourceManager.getString('myResources','COLUMNHEAD_TEXT')}"
									   dataField="text">
						<mx:itemEditor>
							<mx:Component>
								<mx:TextInput maxChars="80"/>
							</mx:Component>
						</mx:itemEditor>
					</mx:DataGridColumn>
					<mx:DataGridColumn id="comboColumn"
									   width="200"
									   dataField="roleId"
									   headerText="{resourceManager.getString('myResources','COLUMNHEAD_ROLE')}"
									   rendererIsEditor="true"
									   labelFunction="{resolveIdToRole}"
									   editorDataField="selectedItemKey">
						<mx:itemRenderer>
							<mx:Component>
								<extenders:GridComboBox dataProvider="{outerDocument.comboData}"
														labelField="charName"
														lookupField="roleId"
														prompt="Select a role..."/>
							</mx:Component>
						</mx:itemRenderer>
					</mx:DataGridColumn>
				</mx:columns>
			</mx:DataGrid>
			<mx:VBox width="100%"
					 height="30%"
					 id="subtitleDetailHolder"
					 horizontalAlign="center"
					 verticalAlign="middle">
				<mx:HBox width="100%"
						 id="subtitleDetailTimesHolder"
						 horizontalAlign="center"
						 verticalAlign="middle"
						 paddingBottom="4"
						 paddingTop="4">
					<mx:HBox height="100%"
							 verticalAlign="middle"
							 horizontalAlign="center"
							 paddingLeft="4"
							 paddingRight="4"
							 borderStyle="solid"
							 cornerRadius="8"
							 backgroundColor="#DDDDDD">
						<mx:Button label="{resourceManager.getString('myResources','OPTION_ADD_SUBTITLE_LINE')}"
								   paddingLeft="6"
								   paddingRight="6"
								   cornerRadius="8"
								   enabled="{true}"
								   icon="@Embed(source='../../resources/images/add.png')"
								   click="subtitleStartHandler(new SubtitlingEvent(SubtitlingEvent.START, VP.streamTime))"
								   id="addSubtitleLine"/>
						<mx:Button label="{resourceManager.getString('myResources','OPTION_REMOVE_SUBTITLE_LINE')}"
								   paddingLeft="6"
								   paddingRight="6"
								   cornerRadius="8"
								   enabled="{subtitleCollection.length > 0}"
								   click="subtitleRemoveHandler()">
							<mx:icon>@Embed(source='../../resources/images/delete.png')</mx:icon>
						</mx:Button>
						<mx:Button label="{resourceManager.getString('myResources','OPTION_CLEAR_SUBTITLE_LINES')}"
								   paddingLeft="6"
								   paddingRight="6"
								   cornerRadius="8"
								   enabled="{subtitleCollection.length > 0}"
								   click="subtitleClearHandler()">
							<mx:icon>@Embed(source='../../resources/images/cancel.png')</mx:icon>
						</mx:Button>
						<mx:VRule height="30"/>
						<mx:Button label="{resourceManager.getString('myResources','OPTION_SAVE_SUBTITLE_LINES')}"
								   paddingLeft="6"
								   paddingRight="6"
								   cornerRadius="8"
								   icon="@Embed(source='../../resources/images/save.png')"
								   click="saveSubtitlesHandler()"/>
					</mx:HBox>
				</mx:HBox>

			</mx:VBox>
		</mx:Panel>
		<mx:HBox width="100%"
				 height="100%">

		</mx:HBox>
	</mx:ViewStack>
</mx:HBox>
