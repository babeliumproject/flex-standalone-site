<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009"
		  xmlns:s="library://ns.adobe.com/flex/spark"
		  xmlns:mx="library://ns.adobe.com/flex/mx"
		  xmlns:main="modules.main.*"
		  width="100%">

	<fx:Script>
		<![CDATA[
			
			import model.DataModel;
			
			import modules.search.VideoPaginator;
			
			import mx.collections.ArrayCollection;
			import mx.events.ListEvent;
			
			import spark.components.Button;

			[Bindable]
			private var _currentPage:int=1;
			
			[Bindable]
			private var _exercisesWithoutSubtitlesList:ArrayCollection;
			
			public function set currentPaginationPage(value:uint):void{
				_currentPage = value;
				createPagination();
			}
			
			public function set dataProvider(exercisesToSubtitle:ArrayCollection):void
			{
				_exercisesWithoutSubtitlesList=exercisesToSubtitle;
				if (_exercisesWithoutSubtitlesList.length > 0)
				{
					exercisesWithoutSubtitlesTile.includeInLayout=true;
					exercisesWithoutSubtitlesTile.visible=true;
					createPagination();
				}
				else
				{
					exercisesWithoutSubtitlesTile.includeInLayout=false;
					exercisesWithoutSubtitlesTile.visible=false;
				}
			}
			
			public function set changeListener(changeFunction:Function):void
			{
				exercisesWithoutSubtitlesTile.addEventListener(ListEvent.CHANGE, changeFunction);
			}
			
			public function createPagination():void
			{
				VideoPaginator.createPaginationMenu(_exercisesWithoutSubtitlesList.length, DataModel.getInstance().pageSize, _currentPage, DataModel.getInstance().numberOfPagesNav, paginationBox, navigateToPage);
				refreshDataProvider();
			}
			
			private function navigateToPage(event:MouseEvent):void
			{
				//Go to the page
				_currentPage=int((event.target as Button).id);
				//Refresh the pagination controls
				createPagination();
			}
			
			public function refreshDataProvider():void
			{
				var current:int=_currentPage - 1;
				var pageSize:int=DataModel.getInstance().pageSize;
				var dataTemp:ArrayCollection=new ArrayCollection(_exercisesWithoutSubtitlesList.source.slice((current * pageSize), (current * pageSize) + pageSize));
				exercisesWithoutSubtitlesTile.dataProvider=dataTemp;
			}
		]]>
	</fx:Script>

	<s:List id="exercisesWithoutSubtitlesTile"
			width="100%"
			minHeight="0"
			borderVisible="false"
			skinClass="skins.TransparentBgListSkin"
			itemRenderer="modules.exercises.ExerciseItemRenderer"
			dataProvider="{_exercisesWithoutSubtitlesList}">
		<s:layout>
			<s:TileLayout horizontalAlign="justify"
						  verticalAlign="middle"
						  columnAlign="justifyUsingWidth"
						  columnWidth="500"/>
		</s:layout>
	</s:List>

	<s:HGroup id="paginationBox"
			  width="100%"
			  horizontalAlign="center"
			  gap="2"/>
</s:VGroup>
