<?xml version="1.0" encoding="utf-8"?>
<s:Module xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  xmlns:mx="library://ns.adobe.com/flex/mx"
		  xmlns:view="modules.create.view.*"
		  width="100%"
		  implements="modules.IModuleInterface" 
		  preinitialize="preinitializeHandler(event)"
		  creationComplete="creationCompleteHandler(event)">
	<s:layout>
		<s:VerticalLayout/>
	</s:layout>

	<fx:Script>
		<![CDATA[
			import com.adobe.cairngorm.control.FrontController;
			
			import control.URLChangeEvent;
			import control.URLManager;
			
			import events.UserEvent;
			
			import flash.utils.getDefinitionByName;
			
			import model.DataModel;
			
			import modules.IGroupInterface;
			import modules.create.command.CreateController;
			import modules.create.command.GetLatestCreations;
			import modules.create.event.CreateEvent;
			
			import mx.binding.utils.BindingUtils;
			import mx.binding.utils.ChangeWatcher;
			import mx.charts.CategoryAxis;
			import mx.core.Application;
			import mx.core.FlexGlobals;
			import mx.events.FlexEvent;

			public var modulename:String = 'create';
			
			[Bindable]
			private var _urlmanager:URLManager=URLManager.getInstance();
			
			[Bindable]
			private var _model:DataModel=DataModel.getInstance();
			
			[Bindable]
			private var _viewstackIndex:uint;
			
			private var _moduleController:CreateController;
			private var _creationComplete:Boolean;
			private var _changewatcher:ChangeWatcher;
			private var _initialUrl:Event;
			
			public static const INDEX:uint=0;
			public static const MANAGE:uint=1;
			public static const EDIT:uint=2;
			
			public function getModuleName():String{
				return modulename;
			}
			
			public function setDataBindings(data:* = null):void{	
				_moduleController = new CreateController();	
				if(data){
					if(data['initialurl']){
						_initialUrl=data['initialurl'];
					}
				}
				//_changewatcher=BindingUtils.bindSetter(onURLChange, _urlmanager, "action", false, true);
			}
			
			public function resetModule():void{
				_urlmanager.removeEventListener(URLChangeEvent.CHANGE, onURLChange);
				_urlmanager=null;
				
				this.removeEventListener(FlexEvent.PREINITIALIZE, preinitializeHandler);
				this.removeEventListener(FlexEvent.CREATION_COMPLETE, creationCompleteHandler);
				this.removeEventListener(FlexEvent.ADD, componentAddedHandler);
				this.removeEventListener(FlexEvent.UPDATE_COMPLETE, componentUpdateCompleteHandler);
				
				_moduleController = null;
				_model = null;
				resetViews();
			}
			
			public function resetViews():void{
				if (createViewstack)
				{
					var views:Array=createViewstack.getChildren();
					for each (var nc:NavigatorContent in views)
					{
						if (nc.deferredContentCreated)
						{
							var v:*=nc.getElementAt(0) as IGroupInterface;
							if (v)
								v.resetGroup();
						}
					}
				}
			}
			
			public function onURLChange(event:Event):void{
				if(!event) return;
				
				var evt:Object=Object(event);

				if(!evt.module || evt.module != modulename) return;
				
				//Reset the components before changing the foreground
				//resetViews();
				
				switch (evt.action)
				{
					case 'manage':
						actionManage(evt.parameters);
						break;
					case 'add':
						actionAddEdit();
						break;
					case 'edit':
						actionAddEdit(evt.parameters);
						break;
					default:
						actionIndex();
						break;
				}
			}
			
			public function actionAddEdit(parameters:Object = null):void{
				var exercise:Object = new Object();
				_viewstackIndex=EDIT;
				
				if(!parameters){
					createEdit.exercisecode=null;
					createEdit.viewIndex=CreateEditView.STEP1;
					new CreateEvent(CreateEvent.EDIT_EXERCISE).dispatch();
				} else {
					var id:String = parameters['id'];
					exercise.exercisecode = id;
					var step:Number = parameters['s'];
					createEdit.exercisecode=id;
					switch(step){
						case 3:
							createEdit.viewIndex=CreateEditView.STEP3;
							break;
						case 2:
							createEdit.viewIndex=CreateEditView.STEP2;
							new CreateEvent(CreateEvent.GET_EXERCISE_MEDIA, exercise).dispatch();
							break;
						default:
							createEdit.viewIndex=CreateEditView.STEP1;
							new CreateEvent(CreateEvent.EDIT_EXERCISE, exercise).dispatch();
							break;
					}
				}
			}
			
			public function actionManage(fragment:Object = null):void{
				_viewstackIndex=MANAGE;
				new CreateEvent(CreateEvent.LIST_CREATIONS).dispatch();
			}
			
			public function actionIndex(fragment:Object = null):void{
				_viewstackIndex=INDEX;
				var params:Object = {'offset':0, 'rowcount':5};
				new CreateEvent(CreateEvent.LIST_LATEST_CREATIONS, params).dispatch(); 
			}
			
			protected function preinitializeHandler(event:FlexEvent):void
			{
				//if(!_model.moduleMap.hasOwnProperty(modulename))
				//	_model.moduleMap[modulename] = new CourseModel();
				
				// The Event.ADDED and Event.ADDED_TO_STAGE events are dispatched before FlexEvent.PREINITIALIZE
				// that leaves only FlexEvent.ADD to know the component has been added as a child somewhere.
				this.addEventListener(FlexEvent.ADD, componentAddedHandler, false, 0, true);
				this.addEventListener(FlexEvent.UPDATE_COMPLETE, componentUpdateCompleteHandler, false, 0, true);
			}
			
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				_creationComplete=true;
				_urlmanager.addEventListener(URLChangeEvent.CHANGE, onURLChange, false, 0, true);
				onURLChange(_initialUrl);
			}
			
			protected function componentAddedHandler(event:FlexEvent):void{
				
				this.visible=false;
			}
			
			protected function componentUpdateCompleteHandler(event:FlexEvent):void{
				checkPermissions();
				
			}
			
			protected function checkPermissions():void{
				if (!DataModel.getInstance().isLoggedIn)
				{
					URLManager.getInstance().redirect('/login');
					return;
				}
				this.visible=true;
			}
		]]>
	</fx:Script>
	
	<mx:ViewStack id="createViewstack"
				  selectedIndex="{ _viewstackIndex }"
				  resizeToContent="true"
				  width="100%"
				  height="100%"
				  creationPolicy="all">
		<s:NavigatorContent id="index">
			<view:CreateIndexView id="createIndex" width="100%"/>
		</s:NavigatorContent>
		<s:NavigatorContent id="manage">
			<view:CreateManageView id="createManage" width="100%"/>
		</s:NavigatorContent>
		<s:NavigatorContent id="edit">
			<view:CreateEditView id="createEdit" width="100%"/>
		</s:NavigatorContent>
	</mx:ViewStack>
	
</s:Module>
