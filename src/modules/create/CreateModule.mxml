<?xml version="1.0" encoding="utf-8"?>
<s:Module xmlns:fx="http://ns.adobe.com/mxml/2009" 
		  xmlns:s="library://ns.adobe.com/flex/spark" 
		  xmlns:mx="library://ns.adobe.com/flex/mx"
		  xmlns:view="modules.create.view.*"
		  width="100%"
		  implements="modules.IModuleInterface" 
		  preinitialize="preinitializeHandler(event)"
		  creationComplete="creationCompleteHandler(event)">
	<s:layout>
		<s:VerticalLayout/>
	</s:layout>
	<fx:Declarations>
		<!-- Place non-visual elements (e.g., services, value objects) here -->
	</fx:Declarations>

	<fx:Script>
		<![CDATA[
			import com.adobe.cairngorm.control.FrontController;
			
			import control.BabeliaBrowserManager;
			
			import events.UserEvent;
			
			import flash.utils.getDefinitionByName;
			
			import model.DataModel;
			
			import modules.IGroupInterface;
			import modules.create.command.CreateController;
			import modules.create.command.GetLatestCreations;
			import modules.create.event.CreateEvent;
			
			import mx.binding.utils.BindingUtils;
			import mx.binding.utils.ChangeWatcher;
			import mx.charts.CategoryAxis;
			import mx.core.Application;
			import mx.core.FlexGlobals;
			import mx.events.FlexEvent;

			public var modulename:String = 'create';
			
			[Bindable]
			private var _urlmanager:BabeliaBrowserManager=BabeliaBrowserManager.getInstance();
			
			[Bindable]
			private var _model:DataModel=DataModel.getInstance();
			
			private var _moduleController:CreateController;
			
			[Bindable]
			private var _viewstackIndex:uint;
			
			private var _creationComplete:Boolean;
			
			public static const INDEX:uint=0;
			public static const MANAGE:uint=1;
			public static const EDIT:uint=2;
			
			private var changewatcher:ChangeWatcher;
			
			public function getModuleName():String{
				return modulename;
			}
			
			public function setDataBindings(data:* = null):void{	
				_moduleController = new CreateController();
				changewatcher=BindingUtils.bindSetter(onURLChange, _urlmanager, "action", false, true);
			}
			
			public function resetModule():void{
				if(changewatcher && changewatcher.isWatching()){
					changewatcher.unwatch();
				}
				changewatcher=null;
				_urlmanager=null;
				
				this.removeEventListener(FlexEvent.PREINITIALIZE, preinitializeHandler);
				this.removeEventListener(FlexEvent.CREATION_COMPLETE, creationCompleteHandler);
				this.removeEventListener(FlexEvent.ADD, componentAddedHandler);
				this.removeEventListener(FlexEvent.UPDATE_COMPLETE, componentUpdateCompleteHandler);
				
				_moduleController = null;
				_model = null;
				resetViews();
			}
			
			public function resetViews():void{
				if (createViewstack)
				{
					var views:Array=createViewstack.getChildren();
					for each (var nc:NavigatorContent in views)
					{
						if (nc.deferredContentCreated)
						{
							var v:*=nc.getElementAt(0) as IGroupInterface;
							if (v)
								v.resetGroup();
						}
					}
				}
			}
			
			public function onURLChange(value:String):void{
				//if (_urlmanager.moduleName != modulename)
				//	return;
				
				resetViews();
				switch (value)
				{
					case 'manage':
						actionManage(_urlmanager.parameters);
						break;
					case 'add':
						actionAddEdit();
						break;
					case 'edit':
						actionAddEdit(_urlmanager.parameters);
						break;
					default:
						actionIndex();
						break;
				}
			}
			
			public function actionAddEdit(parameters:String = null):void{
				
				if(!parameters){
					//Adding new exercise create/add: go to step1
				} else {
					//Editing an existing exercise
					//if create/edit/<exercisecode>: go to step1
					//if create/edit/<exercisecode>?s=1: go to step1
					//if create/edit/<exercisecode>?s=2: go to step2
					//if create/edit/<exercisecode>?s=3: go to step3
				}
				
				trace("create edit call");
				_viewstackIndex=EDIT;
				var exercise:Object = new Object();
				exercise.exercisecode = parameters;
				new CreateEvent(CreateEvent.EDIT_EXERCISE, exercise).dispatch();
			}
			
			public function actionManage(fragment:String = null):void{
				trace("create manage call");
				_viewstackIndex=MANAGE;
			}
			
			public function actionIndex(fragment:String = null):void{
				trace("create index call");
				_viewstackIndex=INDEX;
				
				var params:Object = {'offset':0, 'rowcount':5};
				new CreateEvent(CreateEvent.LIST_LATEST_CREATIONS, params).dispatch(); 
			}
			
			protected function preinitializeHandler(event:FlexEvent):void
			{
				//if(!_model.moduleMap.hasOwnProperty(modulename))
				//	_model.moduleMap[modulename] = new CourseModel();
				
				// The Event.ADDED and Event.ADDED_TO_STAGE events are dispatched before FlexEvent.PREINITIALIZE
				// that leaves only FlexEvent.ADD to know the component has been added as a child somewhere.
				this.addEventListener(FlexEvent.ADD, componentAddedHandler, false, 0, true);
				this.addEventListener(FlexEvent.UPDATE_COMPLETE, componentUpdateCompleteHandler, false, 0, true);
			}
			
			protected function creationCompleteHandler(event:FlexEvent):void
			{
				_creationComplete=true;
			}
			
			protected function componentAddedHandler(event:FlexEvent):void{
				
				this.visible=false;
			}
			
			protected function componentUpdateCompleteHandler(event:FlexEvent):void{
				checkPermissions();
			}
			
			protected function checkPermissions():void{
				if (!DataModel.getInstance().isLoggedIn)
				{
					BabeliaBrowserManager.getInstance().redirect('/login');
					return;
				}
				this.visible=true;
			}
		]]>
	</fx:Script>
	
	<mx:ViewStack id="createViewstack"
				  selectedIndex="{ _viewstackIndex }"
				  resizeToContent="true"
				  width="100%"
				  height="100%">
		<s:NavigatorContent id="index">
			<view:CreateIndexView id="createIndex" width="100%"/>
		</s:NavigatorContent>
		<s:NavigatorContent id="manage">
			<view:CreateManageView id="createManage" width="100%"/>
		</s:NavigatorContent>
		<s:NavigatorContent id="edit">
			<view:CreateEditView id="createEdit" width="100%"/>
		</s:NavigatorContent>
	</mx:ViewStack>
	
</s:Module>
